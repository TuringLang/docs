{
  "cells": [
    {
      "outputs": [],
      "cell_type": "code",
      "source": "# Install necessary dependencies.\nusing Pkg\nPkg.activate(; temp=true)\nPkg.add([])",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n#| echo: false\n#| output: false\nusing Pkg;\nPkg.instantiate();\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "This notebook is ported from the [example notebook](https://www.pymc.io/projects/examples/en/latest/generalized_linear_models/GLM-poisson-regression.html) of PyMC3 on Poisson Regression.\n\n[Poisson Regression](https://en.wikipedia.org/wiki/Poisson_regression) is a technique commonly used to model count data.\nSome of the applications include predicting the number of people defaulting on their loans or the number of cars running on a highway on a given day.\nThis example describes a method to implement the Bayesian version of this technique using Turing.\n\nWe will generate the dataset that we will be working on which describes the relationship between number of times a person sneezes during the day with his alcohol consumption and medicinal intake.\n\nWe start by importing the required libraries.",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n#Import Turing, Distributions and DataFrames\nusing Turing, Distributions, DataFrames, Distributed\n\n# Import MCMCChain, Plots, and StatsPlots for visualisations and diagnostics.\nusing MCMCChains, Plots, StatsPlots\n\n# Set a seed for reproducibility.\nusing Random\nRandom.seed!(12);\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "# Generating data\n\nWe start off by creating a toy dataset. We take the case of a person who takes medicine to prevent excessive sneezing. Alcohol consumption increases the rate of sneezing for that person. Thus, the two factors affecting the number of sneezes in a given day are alcohol consumption and whether the person has taken his medicine. Both these variables are taken as boolean valued while the number of sneezes will be a count valued variable. We also take into consideration that the interaction between the two boolean variables will affect the number of sneezes\n\n5 random rows are printed from the generated data to get a gist of the data generated.",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\ntheta_noalcohol_meds = 1    # no alcohol, took medicine\ntheta_alcohol_meds = 3      # alcohol, took medicine\ntheta_noalcohol_nomeds = 6  # no alcohol, no medicine\ntheta_alcohol_nomeds = 36   # alcohol, no medicine\n\n# no of samples for each of the above cases\nq = 100\n\n#Generate data from different Poisson distributions\nnoalcohol_meds = Poisson(theta_noalcohol_meds)\nalcohol_meds = Poisson(theta_alcohol_meds)\nnoalcohol_nomeds = Poisson(theta_noalcohol_nomeds)\nalcohol_nomeds = Poisson(theta_alcohol_nomeds)\n\nnsneeze_data = vcat(\n    rand(noalcohol_meds, q),\n    rand(alcohol_meds, q),\n    rand(noalcohol_nomeds, q),\n    rand(alcohol_nomeds, q),\n)\nalcohol_data = vcat(zeros(q), ones(q), zeros(q), ones(q))\nmeds_data = vcat(zeros(q), zeros(q), ones(q), ones(q))\n\ndf = DataFrame(;\n    nsneeze=nsneeze_data,\n    alcohol_taken=alcohol_data,\n    nomeds_taken=meds_data,\n    product_alcohol_meds=meds_data .* alcohol_data,\n)\ndf[sample(1:nrow(df), 5; replace=false), :]\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "# Visualisation of the dataset\n\nWe plot the distribution of the number of sneezes for the 4 different cases taken above. As expected, the person sneezes the most when he has taken alcohol and not taken his medicine. He sneezes the least when he doesn't consume alcohol and takes his medicine.",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n# Data Plotting\n\np1 = Plots.histogram(\n    df[(df[:, :alcohol_taken] .== 0) .& (df[:, :nomeds_taken] .== 0), 1];\n    title=\"no_alcohol+meds\",\n)\np2 = Plots.histogram(\n    (df[(df[:, :alcohol_taken] .== 1) .& (df[:, :nomeds_taken] .== 0), 1]);\n    title=\"alcohol+meds\",\n)\np3 = Plots.histogram(\n    (df[(df[:, :alcohol_taken] .== 0) .& (df[:, :nomeds_taken] .== 1), 1]);\n    title=\"no_alcohol+no_meds\",\n)\np4 = Plots.histogram(\n    (df[(df[:, :alcohol_taken] .== 1) .& (df[:, :nomeds_taken] .== 1), 1]);\n    title=\"alcohol+no_meds\",\n)\nplot(p1, p2, p3, p4; layout=(2, 2), legend=false)\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "We must convert our `DataFrame` data into the `Matrix` form as the manipulations that we are about are designed to work with `Matrix` data. We also separate the features from the labels which will be later used by the Turing sampler to generate samples from the posterior.",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n# Convert the DataFrame object to matrices.\ndata = Matrix(df[:, [:alcohol_taken, :nomeds_taken, :product_alcohol_meds]])\ndata_labels = df[:, :nsneeze]\ndata\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "We must standardise our data (centring about 0 with unit variance) to help the Turing sampler initialise parameter estimates effectively. We do this by subtracting the mean and dividing by the standard deviation for each column:",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n# Rescale our matrices.\ndata = (data .- mean(data; dims=1)) ./ std(data; dims=1)\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "# Declaring the Model: Poisson Regression\n\nOur model, `poisson_regression` takes four arguments:\n\n  - `x` is our set of independent variables;\n  - `y` is the element we want to predict;\n  - `n` is the number of observations we have; and\n  - `σ²` is the standard deviation we want to assume for our priors.\n\nWithin the model, we create four coefficients (`b0`, `b1`, `b2`, and `b3`) and assign a prior of normally distributed with means of zero and standard deviations of `σ²`. We want to find values of these four coefficients to predict any given `y`.\n\nIntuitively, we can think of the coefficients as:\n\n  - `b1` is the coefficient which represents the effect of taking alcohol on the number of sneezes;\n  - `b2` is the coefficient which represents the effect of taking in no medicines on the number of sneezes;\n  - `b3` is the coefficient which represents the effect of interaction between taking alcohol and no medicine on the number of sneezes;\n\nThe `for` block creates a variable `theta` which is the weighted combination of the input features. We have defined the priors on these weights above. We then observe the likelihood of calculating `theta` given the actual label, `y[i]`.",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n# Bayesian poisson regression (LR)\n@model function poisson_regression(x, y, n, σ²)\n    b0 ~ Normal(0, σ²)\n    b1 ~ Normal(0, σ²)\n    b2 ~ Normal(0, σ²)\n    b3 ~ Normal(0, σ²)\n    for i in 1:n\n        theta = b0 + b1 * x[i, 1] + b2 * x[i, 2] + b3 * x[i, 3]\n        y[i] ~ Poisson(exp(theta))\n    end\nend;\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "# Sampling from the posterior\n\nWe use the `NUTS` sampler to sample values from the posterior. We run multiple chains using the `MCMCThreads()` function to nullify the effect of a problematic chain. We then use the Gelman, Rubin, and Brooks Diagnostic to check the convergence of these multiple chains.",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n#| output: false\n# Retrieve the number of observations.\nn, _ = size(data)\n\n# Sample using NUTS.\n\nnum_chains = 4\nm = poisson_regression(data, data_labels, n, 10)\nchain = sample(m, NUTS(), MCMCThreads(), 2_500, num_chains; discard_adapt=false, progress=false)\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n#| echo: false\nchain\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "> ## Sampling With Multiple Threads\n> The `sample()` call above assumes that you have at least `nchains` threads available in your Julia instance. If you do not, the multiple chains \n> will run sequentially, and you may notice a warning. For more information, see [the Turing documentation on sampling multiple chains.]({{<meta core-functionality>}}#sampling-multiple-chains)\n\n# Viewing the Diagnostics\n\nWe use the Gelman, Rubin, and Brooks Diagnostic to check whether our chains have converged. Note that we require multiple chains to use this diagnostic which analyses the difference between these multiple chains.\n\nWe expect the chains to have converged. This is because we have taken sufficient number of iterations (1500) for the NUTS sampler. However, in case the test fails, then we will have to take a larger number of iterations, resulting in longer computation time.",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n# Because some of the sampler statistics are `missing`, we need to extract only\n# the parameters and then concretize the array so that `gelmandiag` can be computed.\nparameter_chain = MCMCChains.concretize(MCMCChains.get_sections(chain, :parameters))\ngelmandiag(parameter_chain)\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "From the above diagnostic, we can conclude that the chains have converged because the PSRF values of the coefficients are close to 1.\n\nSo, we have obtained the posterior distributions of the parameters. We transform the coefficients and recover theta values by taking the exponent of the meaned values of the coefficients `b0`, `b1`, `b2` and `b3`. We take the exponent of the means to get a better comparison of the relative values of the coefficients. We then compare this with the intuitive meaning that was described earlier.",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "```julia\n# Taking the first chain\nc1 = chain[:, :, 1]\n\n# Calculating the exponentiated means\nb0_exp = exp(mean(c1[:b0]))\nb1_exp = exp(mean(c1[:b1]))\nb2_exp = exp(mean(c1[:b2]))\nb3_exp = exp(mean(c1[:b3]))\n\nprint(\"The exponent of the meaned values of the weights (or coefficients are): \\n\")\nprintln(\"b0: \", b0_exp)\nprintln(\"b1: \", b1_exp)\nprintln(\"b2: \", b2_exp)\nprintln(\"b3: \", b3_exp)\nprint(\"The posterior distributions obtained after sampling can be visualised as :\\n\")\n```",
      "metadata": {}
    },
    {
      "cell_type": "markdown",
      "source": "Visualising the posterior by plotting it:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "plot(chain)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "# Interpreting the Obtained Mean Values\n\nThe exponentiated mean of the coefficient `b1` is roughly half of that of `b2`. This makes sense because in the data that we generated, the number of sneezes was more sensitive to the medicinal intake as compared to the alcohol consumption. We also get a weaker dependence on the interaction between the alcohol consumption and the medicinal intake as can be seen from the value of `b3`.\n\n# Removing the Warmup Samples\n\nAs can be seen from the plots above, the parameters converge to their final distributions after a few iterations.\nThe initial samples during the [warmup phase](https://en.wikipedia.org/wiki/Markov_chain_Monte_Carlo) show higher variability and should be discarded before computing posterior statistics.\nThus, we remove these warmup samples and view the diagnostics again.\nWe discard the first 200 samples, corresponding to the adaptation phase used by the NUTS sampler (which we explicitly chose not to discard earlier with the `discard_adapt=false` argument).",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "chains_new = chain[201:end, :, :]",
      "metadata": {},
      "execution_count": null
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "plot(chains_new)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "As can be seen from the numeric values and the plots above, the standard deviation values have decreased and all the plotted values are from the estimated posteriors. The exponentiated mean values, with the warmup samples removed, have not changed by much and they are still in accordance with their intuitive meanings as described earlier.",
      "metadata": {}
    }
  ],
  "nbformat_minor": 5,
  "metadata": {
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia"
    },
    "kernelspec": {
      "name": "julia",
      "display_name": "Julia",
      "language": "julia"
    }
  },
  "nbformat": 4
}