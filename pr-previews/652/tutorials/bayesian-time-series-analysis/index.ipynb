{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Bayesian Time Series Analysis\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../13-seasonal-time-series/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "In time series analysis we are often interested in understanding how various real-life circumstances impact our quantity of interest.\n",
        "These can be, for instance, season, day of week, or time of day.\n",
        "To analyse this it is useful to decompose time series into simpler components (corresponding to relevant circumstances)\n",
        "and infer their relevance.\n",
        "In this tutorial we are going to use Turing for time series analysis and learn about useful ways to decompose time series.\n",
        "\n",
        "# Modelling time series\n",
        "\n",
        "Before we start coding, let us talk about what exactly we mean with time series decomposition.\n",
        "In a nutshell, it is a divide-and-conquer approach where we express a time series as a sum or a product of simpler series.\n",
        "For instance, the time series $f(t)$ can be decomposed into a sum of $n$ components\n",
        "\n",
        "$$f(t) = \\sum_{i=1}^n f_i(t),$$\n",
        "\n",
        "or we can decompose $g(t)$ into a product of $m$ components\n",
        "\n",
        "$$g(t) = \\prod_{i=1}^m g_i(t).$$\n",
        "\n",
        "We refer to this as *additive* or *multiplicative* decomposition respectively.\n",
        "This type of decomposition is great since it lets us reason about individual components, which makes encoding prior information and interpreting model predictions very easy.\n",
        "Two common components are *trends*, which represent the overall change of the time series (often assumed to be linear),\n",
        "and *cyclic effects* which contribute oscillating effects around the trend.\n",
        "Let us simulate some data with an additive linear trend and oscillating effects.\n",
        "\n",
        "```{julia}\n",
        "using Turing\n",
        "using FillArrays\n",
        "using StatsPlots\n",
        "\n",
        "using LinearAlgebra\n",
        "using Random\n",
        "using Statistics\n",
        "\n",
        "Random.seed!(12345)\n",
        "\n",
        "true_sin_freq = 2\n",
        "true_sin_amp = 5\n",
        "true_cos_freq = 7\n",
        "true_cos_amp = 2.5\n",
        "tmax = 10\n",
        "β_true = 2\n",
        "α_true = -1\n",
        "tt = 0:0.05:tmax\n",
        "f₁(t) = α_true + β_true * t\n",
        "f₂(t) = true_sin_amp * sinpi(2 * t * true_sin_freq / tmax)\n",
        "f₃(t) = true_cos_amp * cospi(2 * t * true_cos_freq / tmax)\n",
        "f(t) = f₁(t) + f₂(t) + f₃(t)\n",
        "\n",
        "plot(f, tt; label=\"f(t)\", title=\"Observed time series\", legend=:topleft, linewidth=3)\n",
        "plot!(\n",
        "    [f₁, f₂, f₃],\n",
        "    tt;\n",
        "    label=[\"f₁(t)\" \"f₂(t)\" \"f₃(t)\"],\n",
        "    style=[:dot :dash :dashdot],\n",
        "    linewidth=1,\n",
        ")\n",
        "```\n",
        "\n",
        "Even though we use simple components, combining them can give rise to fairly complex time series.\n",
        "In this time series, cyclic effects are just added on top of the trend.\n",
        "If we instead multiply the components the cyclic effects cause the series to oscillate\n",
        "between larger and larger values, since they get scaled by the trend.\n",
        "\n",
        "```{julia}\n",
        "g(t) = f₁(t) * f₂(t) * f₃(t)\n",
        "\n",
        "plot(g, tt; label=\"f(t)\", title=\"Observed time series\", legend=:topleft, linewidth=3)\n",
        "plot!([f₁, f₂, f₃], tt; label=[\"f₁(t)\" \"f₂(t)\" \"f₃(t)\"], linewidth=1)\n",
        "```\n",
        "\n",
        "Unlike $f$, $g$ oscillates around $0$ since it is being multiplied with sines and cosines.\n",
        "To let a multiplicative decomposition oscillate around the trend we could define it as\n",
        "$\\tilde{g}(t) = f₁(t) * (1 + f₂(t)) * (1 + f₃(t)),$\n",
        "but for convenience we will leave it as is.\n",
        "The inference machinery is the same for both cases.\n",
        "\n",
        "# Model fitting\n",
        "\n",
        "Having discussed time series decomposition, let us fit a model to the time series above and recover the true parameters.\n",
        "Before building our model, we standardise the time axis to $[0, 1]$ and subtract the max of the time series.\n",
        "This helps convergence while maintaining interpretability and the correct scales for the cyclic components.\n",
        "\n",
        "```{julia}\n",
        "σ_true = 0.35\n",
        "t = collect(tt[begin:3:end])\n",
        "t_min, t_max = extrema(t)\n",
        "x = (t .- t_min) ./ (t_max - t_min)\n",
        "yf = f.(t) .+ σ_true .* randn(size(t))\n",
        "yf_max = maximum(yf)\n",
        "yf = yf .- yf_max\n",
        "\n",
        "scatter(x, yf; title=\"Standardised data\", legend=false)\n",
        "```\n",
        "\n",
        "Let us now build our model.\n",
        "We want to assume a linear trend, and cyclic effects.\n",
        "Encoding a linear trend is easy enough, but what about cyclical effects?\n",
        "We will take a scattergun approach, and create multiple cyclical features using both sine and cosine functions and let our inference machinery figure out which to keep.\n",
        "To do this, we define how long a one period should be, and create features in reference to said period.\n",
        "How long a period should be is problem dependent, but as an example let us say it is $1$ year.\n",
        "If we then find evidence for a cyclic effect with a frequency of 2, that would mean a biannual effect. A frequency of 4 would mean quarterly etc.\n",
        "Since we are using synthetic data, we are simply going to let the period be 1, which is the entire length of the time series.\n",
        "\n",
        "```{julia}\n",
        "freqs = 1:10\n",
        "num_freqs = length(freqs)\n",
        "period = 1\n",
        "cyclic_features = [sinpi.(2 .* freqs' .* x ./ period) cospi.(2 .* freqs' .* x ./ period)]\n",
        "\n",
        "plot_freqs = [1, 3, 5]\n",
        "freq_ptl = plot(\n",
        "    cyclic_features[:, plot_freqs];\n",
        "    label=permutedims([\"sin(2π$(f)x)\" for f in plot_freqs]),\n",
        "    title=\"Cyclical features subset\",\n",
        ")\n",
        "```\n",
        "\n",
        "Having constructed the cyclical features, we can finally build our model. The model we will implement looks like this\n",
        "\n",
        "$$\n",
        "f(t) = \\alpha + \\beta_t t + \\sum_{i=1}^F \\beta_{\\sin{},i} \\sin{}(2\\pi f_i t) + \\sum_{i=1}^F \\beta_{\\cos{},i} \\cos{}(2\\pi f_i t),\n",
        "$$\n",
        "\n",
        "with a Gaussian likelihood $y \\sim \\mathcal{N}(f(t), \\sigma^2)$.\n",
        "For convenience we are treating the cyclical feature weights $\\beta_{\\sin{},i}$ and $\\beta_{\\cos{},i}$ the same in code and weight them with $\\beta_c$.\n",
        "And just because it is so easy, we parameterise our model with the operation with which to apply the cyclic effects.\n",
        "This lets us use the exact same code for both additive and multiplicative models.\n",
        "Finally, we plot prior predictive samples to make sure our priors make sense.\n",
        "\n",
        "```{julia}\n",
        "@model function decomp_model(t, c, op)\n",
        "    α ~ Normal(0, 10)\n",
        "    βt ~ Normal(0, 2)\n",
        "    βc ~ MvNormal(Zeros(size(c, 2)), I)\n",
        "    σ ~ truncated(Normal(0, 0.1); lower=0)\n",
        "\n",
        "    cyclic = c * βc\n",
        "    trend = α .+ βt .* t\n",
        "    μ = op(trend, cyclic)\n",
        "    y ~ MvNormal(μ, σ^2 * I)\n",
        "    return (; trend, cyclic)\n",
        "end\n",
        "\n",
        "y_prior_samples = mapreduce(hcat, 1:100) do _\n",
        "    rand(decomp_model(t, cyclic_features, +)).y\n",
        "end\n",
        "plot(t, y_prior_samples; linewidth=1, alpha=0.5, color=1, label=\"\", title=\"Prior samples\")\n",
        "scatter!(t, yf; color=2, label=\"Data\")\n",
        "```\n",
        "\n",
        "With the model specified and with a reasonable prior we can now let Turing decompose the time series for us!\n",
        "\n",
        "```{julia}\n",
        "using MCMCChains: get_sections\n",
        "\n",
        "function mean_ribbon(samples)\n",
        "    qs = quantile(samples)\n",
        "    low = qs[:, Symbol(\"2.5%\")]\n",
        "    up = qs[:, Symbol(\"97.5%\")]\n",
        "    m = mean(samples)[:, :mean]\n",
        "    return m, (m - low, up - m)\n",
        "end\n",
        "\n",
        "function get_decomposition(model, x, cyclic_features, chain, op)\n",
        "    chain_params = get_sections(chain, :parameters)\n",
        "    return returned(model(x, cyclic_features, op), chain_params)\n",
        "end\n",
        "\n",
        "function plot_fit(x, y, decomp, ymax)\n",
        "    trend = mapreduce(x -> x.trend, hcat, decomp)\n",
        "    cyclic = mapreduce(x -> x.cyclic, hcat, decomp)\n",
        "\n",
        "    trend_plt = plot(\n",
        "        x,\n",
        "        trend .+ ymax;\n",
        "        color=1,\n",
        "        label=nothing,\n",
        "        alpha=0.2,\n",
        "        title=\"Trend\",\n",
        "        xlabel=\"Time\",\n",
        "        ylabel=\"f₁(t)\",\n",
        "    )\n",
        "    ls = [ones(length(t)) t] \\ y\n",
        "    α̂, β̂ = ls[1], ls[2:end]\n",
        "    plot!(\n",
        "        trend_plt,\n",
        "        t,\n",
        "        α̂ .+ t .* β̂ .+ ymax;\n",
        "        label=\"Least squares trend\",\n",
        "        color=5,\n",
        "        linewidth=4,\n",
        "    )\n",
        "\n",
        "    scatter!(trend_plt, x, y .+ ymax; label=nothing, color=2, legend=:topleft)\n",
        "    cyclic_plt = plot(\n",
        "        x,\n",
        "        cyclic;\n",
        "        color=1,\n",
        "        label=nothing,\n",
        "        alpha=0.2,\n",
        "        title=\"Cyclic effect\",\n",
        "        xlabel=\"Time\",\n",
        "        ylabel=\"f₂(t)\",\n",
        "    )\n",
        "    return trend_plt, cyclic_plt\n",
        "end\n",
        "\n",
        "chain = sample(decomp_model(x, cyclic_features, +) | (; y=yf), NUTS(), 2000, progress=false)\n",
        "yf_samples = predict(decomp_model(x, cyclic_features, +), chain)\n",
        "m, conf = mean_ribbon(yf_samples)\n",
        "predictive_plt = plot(\n",
        "    t,\n",
        "    m .+ yf_max;\n",
        "    ribbon=conf,\n",
        "    label=\"Posterior density\",\n",
        "    title=\"Posterior decomposition\",\n",
        "    xlabel=\"Time\",\n",
        "    ylabel=\"f(t)\",\n",
        ")\n",
        "scatter!(predictive_plt, t, yf .+ yf_max; color=2, label=\"Data\", legend=:topleft)\n",
        "\n",
        "decomp = get_decomposition(decomp_model, x, cyclic_features, chain, +)\n",
        "decomposed_plt = plot_fit(t, yf, decomp, yf_max)\n",
        "plot(predictive_plt, decomposed_plt...; layout=(3, 1), size=(700, 1000))\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "let\n",
        "    @assert mean(ess(chain)[:, :ess]) > 500 \"Mean ESS: $(mean(ess(chain)[:, :ess])) - not > 500\"\n",
        "    lower_quantile = m .- conf[1] # 2.5% quantile\n",
        "    upper_quantile = m .+ conf[2] # 97.5% quantile\n",
        "    @assert mean(lower_quantile .≤ yf .≤ upper_quantile) ≥ 0.9 \"Surprisingly few observations in predicted 95% interval: $(mean(lower_quantile .≤ yf .≤ upper_quantile))\"\n",
        "end\n",
        "```\n",
        "\n",
        "Inference is successful and the posterior beautifully captures the data.\n",
        "We see that the least squares linear fit deviates somewhat from the posterior trend.\n",
        "Since our model takes cyclic effects into account separately,\n",
        "we get a better estimate of the true overall trend than if we would have just fitted a line.\n",
        "But what frequency content did the model identify?\n",
        "\n",
        "\n",
        "```{julia}\n",
        "function plot_cyclic_features(βsin, βcos)\n",
        "    labels = reshape([\"freq = $i\" for i in freqs], 1, :)\n",
        "    colors = collect(freqs)'\n",
        "    style = reshape([i <= 10 ? :solid : :dash for i in 1:length(labels)], 1, :)\n",
        "    sin_features_plt = density(\n",
        "        βsin[:, :, 1];\n",
        "        title=\"Sine features posterior\",\n",
        "        label=labels,\n",
        "        ylabel=\"Density\",\n",
        "        xlabel=\"Weight\",\n",
        "        color=colors,\n",
        "        linestyle=style,\n",
        "        legend=nothing,\n",
        "    )\n",
        "    cos_features_plt = density(\n",
        "        βcos[:, :, 1];\n",
        "        title=\"Cosine features posterior\",\n",
        "        ylabel=\"Density\",\n",
        "        xlabel=\"Weight\",\n",
        "        label=nothing,\n",
        "        color=colors,\n",
        "        linestyle=style,\n",
        "    )\n",
        "\n",
        "    return seasonal_features_plt = plot(\n",
        "        sin_features_plt,\n",
        "        cos_features_plt;\n",
        "        layout=(2, 1),\n",
        "        size=(800, 600),\n",
        "        legend=:outerright,\n",
        "    )\n",
        "end\n",
        "\n",
        "βc = Array(group(chain, :βc))\n",
        "plot_cyclic_features(βc[:, begin:num_freqs, :], βc[:, (num_freqs + 1):end, :])\n",
        "```\n",
        "\n",
        "Plotting the posterior over the cyclic features reveals that the model managed to extract the true frequency content.\n",
        "\n",
        "Since we wrote our model to accept a combining operator, we can easily run the same analysis for a multiplicative model.\n",
        "\n",
        "```{julia}\n",
        "yg = g.(t) .+ σ_true .* randn(size(t))\n",
        "\n",
        "y_prior_samples = mapreduce(hcat, 1:100) do _\n",
        "    rand(decomp_model(t, cyclic_features, .*)).y\n",
        "end\n",
        "plot(t, y_prior_samples; linewidth=1, alpha=0.5, color=1, label=\"\", title=\"Prior samples\")\n",
        "scatter!(t, yf; color=2, label=\"Data\")\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "chain = sample(decomp_model(x, cyclic_features, .*) | (; y=yg), NUTS(), 2000, progress=false)\n",
        "yg_samples = predict(decomp_model(x, cyclic_features, .*), chain)\n",
        "m, conf = mean_ribbon(yg_samples)\n",
        "predictive_plt = plot(\n",
        "    t,\n",
        "    m;\n",
        "    ribbon=conf,\n",
        "    label=\"Posterior density\",\n",
        "    title=\"Posterior decomposition\",\n",
        "    xlabel=\"Time\",\n",
        "    ylabel=\"g(t)\",\n",
        ")\n",
        "scatter!(predictive_plt, t, yg; color=2, label=\"Data\", legend=:topleft)\n",
        "\n",
        "decomp = get_decomposition(decomp_model, x, cyclic_features, chain, .*)\n",
        "decomposed_plt = plot_fit(t, yg, decomp, 0)\n",
        "plot(predictive_plt, decomposed_plt...; layout=(3, 1), size=(700, 1000))\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "let\n",
        "    @assert mean(ess(chain)[:, :ess]) > 500 \"Mean ESS: $(mean(ess(chain)[:, :ess])) - not > 500\"\n",
        "    lower_quantile = m .- conf[1] # 2.5% quantile\n",
        "    upper_quantile = m .+ conf[2] # 97.5% quantile\n",
        "    @assert mean(lower_quantile .≤ yg .≤ upper_quantile) ≥ 0.9 \"Surprisingly few observations in predicted 95% interval: $(mean(lower_quantile .≤ yg .≤ upper_quantile))\"\n",
        "end\n",
        "```\n",
        "\n",
        "The model fits! What about the infered cyclic components?\n",
        "\n",
        "```{julia}\n",
        "βc = Array(group(chain, :βc))\n",
        "plot_cyclic_features(βc[:, begin:num_freqs, :], βc[:, (num_freqs + 1):end, :])\n",
        "```\n",
        "\n",
        "While multiplicative model fits to the data, it does not recover the true parameters for this dataset.\n",
        "\n",
        "# Wrapping up\n",
        "\n",
        "In this tutorial we have seen how to implement and fit time series models using additive and multiplicative decomposition.\n",
        "We also saw how to visualise the model fit, and how to interpret learned cyclical components."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}