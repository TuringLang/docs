{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Bayesian Linear Regression\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../05-linear-regression/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "Turing is powerful when applied to complex hierarchical models, but it can also be put to task at common statistical procedures, like [linear regression](https://en.wikipedia.org/wiki/Linear_regression).\n",
        "This tutorial covers how to implement a linear regression model in Turing.\n",
        "\n",
        "## Set Up\n",
        "\n",
        "We begin by importing all the necessary libraries.\n",
        "\n",
        "```{julia}\n",
        "# Import Turing.\n",
        "using Turing\n",
        "\n",
        "# Package for loading the data set.\n",
        "using RDatasets\n",
        "\n",
        "# Package for visualization.\n",
        "using StatsPlots\n",
        "\n",
        "# Functionality for splitting the data.\n",
        "using MLUtils: splitobs\n",
        "\n",
        "# Functionality for constructing arrays with identical elements efficiently.\n",
        "using FillArrays\n",
        "\n",
        "# Functionality for normalizing the data and evaluating the model predictions.\n",
        "using StatsBase\n",
        "\n",
        "# Functionality for working with scaled identity matrices.\n",
        "using LinearAlgebra\n",
        "\n",
        "# Set a seed for reproducibility.\n",
        "using Random\n",
        "Random.seed!(0);\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "setprogress!(false)\n",
        "```\n",
        "\n",
        "We will use the `mtcars` dataset from the [RDatasets](https://github.com/JuliaStats/RDatasets.jl) package.\n",
        "`mtcars` contains a variety of statistics on different car models, including their miles per gallon, number of cylinders, and horsepower, among others.\n",
        "\n",
        "We want to know if we can construct a Bayesian linear regression model to predict the miles per gallon of a car, given the other statistics it has.\n",
        "Let us take a look at the data we have.\n",
        "\n",
        "```{julia}\n",
        "# Load the dataset.\n",
        "data = RDatasets.dataset(\"datasets\", \"mtcars\")\n",
        "\n",
        "# Show the first six rows of the dataset.\n",
        "first(data, 6)\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "size(data)\n",
        "```\n",
        "\n",
        "The next step is to get our data ready for testing. We'll split the `mtcars` dataset into two subsets, one for training our model and one for evaluating our model. Then, we separate the targets we want to learn (`MPG`, in this case) and standardize the datasets by subtracting each column's means and dividing by the standard deviation of that column. The resulting data is not very familiar looking, but this standardization process helps the sampler converge far easier.\n",
        "\n",
        "```{julia}\n",
        "# Remove the model column.\n",
        "select!(data, Not(:Model))\n",
        "\n",
        "# Split our dataset 70%/30% into training/test sets.\n",
        "trainset, testset = map(DataFrame, splitobs(data; at=0.7, shuffle=true))\n",
        "\n",
        "# Turing requires data in matrix form.\n",
        "target = :MPG\n",
        "train = Matrix(select(trainset, Not(target)))\n",
        "test = Matrix(select(testset, Not(target)))\n",
        "train_target = trainset[:, target]\n",
        "test_target = testset[:, target]\n",
        "\n",
        "# Standardize the features.\n",
        "dt_features = fit(ZScoreTransform, train; dims=1)\n",
        "StatsBase.transform!(dt_features, train)\n",
        "StatsBase.transform!(dt_features, test)\n",
        "\n",
        "# Standardize the targets.\n",
        "dt_targets = fit(ZScoreTransform, train_target)\n",
        "StatsBase.transform!(dt_targets, train_target)\n",
        "StatsBase.transform!(dt_targets, test_target);\n",
        "```\n",
        "\n",
        "## Model Specification\n",
        "\n",
        "In a traditional frequentist model using [OLS](https://en.wikipedia.org/wiki/Ordinary_least_squares), our model might look like:\n",
        "\n",
        "$$\n",
        "\\mathrm{MPG}_i = \\alpha + \\boldsymbol{\\beta}^\\mathsf{T}\\boldsymbol{X_i}\n",
        "$$\n",
        "\n",
        "where $\\boldsymbol{\\beta}$ is a vector of coefficients and $\\boldsymbol{X}$ is a vector of inputs for observation $i$. The Bayesian model we are more concerned with is the following:\n",
        "\n",
        "$$\n",
        "\\mathrm{MPG}_i \\sim \\mathcal{N}(\\alpha + \\boldsymbol{\\beta}^\\mathsf{T}\\boldsymbol{X_i}, \\sigma^2)\n",
        "$$\n",
        "\n",
        "where $\\alpha$ is an intercept term common to all observations, $\\boldsymbol{\\beta}$ is a coefficient vector, $\\boldsymbol{X_i}$ is the observed data for car $i$, and $\\sigma^2$ is a common variance term.\n",
        "\n",
        "For $\\sigma^2$, we assign a prior of `truncated(Normal(0, 100); lower=0)`.\n",
        "This is consistent with [Andrew Gelman's recommendations](http://www.stat.columbia.edu/%7Egelman/research/published/taumain.pdf) on noninformative priors for variance.\n",
        "The intercept term ($\\alpha$) is assumed to be normally distributed with a mean of zero and a variance of three.\n",
        "This represents our assumptions that miles per gallon can be explained mostly by our assorted variables, but a high variance term indicates our uncertainty about that.\n",
        "Each coefficient is assumed to be normally distributed with a mean of zero and a variance of 10.\n",
        "We do not know that our coefficients are different from zero, and we don't know which ones are likely to be the most important, so the variance term is quite high.\n",
        "Lastly, each observation $y_i$ is distributed according to the calculated `mu` term given by $\\alpha + \\boldsymbol{\\beta}^\\mathsf{T}\\boldsymbol{X_i}$.\n",
        "\n",
        "```{julia}\n",
        "# Bayesian linear regression.\n",
        "@model function linear_regression(x, y)\n",
        "    # Set variance prior.\n",
        "    σ² ~ truncated(Normal(0, 100); lower=0)\n",
        "\n",
        "    # Set intercept prior.\n",
        "    intercept ~ Normal(0, sqrt(3))\n",
        "\n",
        "    # Set the priors on our coefficients.\n",
        "    nfeatures = size(x, 2)\n",
        "    coefficients ~ MvNormal(Zeros(nfeatures), 10.0 * I)\n",
        "\n",
        "    # Calculate all the mu terms.\n",
        "    mu = intercept .+ x * coefficients\n",
        "    return y ~ MvNormal(mu, σ² * I)\n",
        "end\n",
        "```\n",
        "\n",
        "With our model specified, we can call the sampler. We will use the No U-Turn Sampler ([NUTS](https://turinglang.org/stable/docs/library/#Turing.Inference.NUTS)) here.\n",
        "\n",
        "```{julia}\n",
        "model = linear_regression(train, train_target)\n",
        "chain = sample(model, NUTS(), 5_000)\n",
        "```\n",
        "\n",
        "We can also check the densities and traces of the parameters visually using the `plot` functionality.\n",
        "\n",
        "```{julia}\n",
        "plot(chain)\n",
        "```\n",
        "\n",
        "It looks like all parameters have converged.\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "let\n",
        "    ess_df = ess(chain)\n",
        "    @assert minimum(ess_df[:, :ess]) > 500 \"Minimum ESS: $(minimum(ess_df[:, :ess])) - not > 700\"\n",
        "    @assert mean(ess_df[:, :ess]) > 2_000 \"Mean ESS: $(mean(ess_df[:, :ess])) - not > 2000\"\n",
        "    @assert maximum(ess_df[:, :ess]) > 3_500 \"Maximum ESS: $(maximum(ess_df[:, :ess])) - not > 3500\"\n",
        "end\n",
        "```\n",
        "\n",
        "## Comparing to OLS\n",
        "\n",
        "A satisfactory test of our model is to evaluate how well it predicts. Importantly, we want to compare our model to existing tools like OLS. The code below uses the [GLM.jl](https://juliastats.org/GLM.jl/stable/) package to generate a traditional OLS multiple regression model on the same data as our probabilistic model.\n",
        "\n",
        "```{julia}\n",
        "# Import the GLM package.\n",
        "using GLM\n",
        "\n",
        "# Perform multiple regression OLS.\n",
        "train_with_intercept = hcat(ones(size(train, 1)), train)\n",
        "ols = lm(train_with_intercept, train_target)\n",
        "\n",
        "# Compute predictions on the training data set and unstandardize them.\n",
        "train_prediction_ols = GLM.predict(ols)\n",
        "StatsBase.reconstruct!(dt_targets, train_prediction_ols)\n",
        "\n",
        "# Compute predictions on the test data set and unstandardize them.\n",
        "test_with_intercept = hcat(ones(size(test, 1)), test)\n",
        "test_prediction_ols = GLM.predict(ols, test_with_intercept)\n",
        "StatsBase.reconstruct!(dt_targets, test_prediction_ols);\n",
        "```\n",
        "\n",
        "The function below accepts a chain and an input matrix and calculates predictions. We use the samples of the model parameters in the chain starting with sample 200.\n",
        "\n",
        "```{julia}\n",
        "# Make a prediction given an input vector.\n",
        "function prediction(chain, x)\n",
        "    p = get_params(chain[200:end, :, :])\n",
        "    targets = p.intercept' .+ x * reduce(hcat, p.coefficients)'\n",
        "    return vec(mean(targets; dims=2))\n",
        "end\n",
        "```\n",
        "\n",
        "When we make predictions, we unstandardize them so they are more understandable.\n",
        "\n",
        "```{julia}\n",
        "# Calculate the predictions for the training and testing sets and unstandardize them.\n",
        "train_prediction_bayes = prediction(chain, train)\n",
        "StatsBase.reconstruct!(dt_targets, train_prediction_bayes)\n",
        "test_prediction_bayes = prediction(chain, test)\n",
        "StatsBase.reconstruct!(dt_targets, test_prediction_bayes)\n",
        "\n",
        "# Show the predictions on the test data set.\n",
        "DataFrame(; MPG=testset[!, target], Bayes=test_prediction_bayes, OLS=test_prediction_ols)\n",
        "```\n",
        "\n",
        "Now let's evaluate the loss for each method, and each prediction set. We will use the mean squared error to evaluate loss, given by\n",
        "$$\n",
        "\\mathrm{MSE} = \\frac{1}{n} \\sum_{i=1}^n {(y_i - \\hat{y_i})^2}\n",
        "$$\n",
        "where $y_i$ is the actual value (true MPG) and $\\hat{y_i}$ is the predicted value using either OLS or Bayesian linear regression. A lower SSE indicates a closer fit to the data.\n",
        "\n",
        "```{julia}\n",
        "println(\n",
        "    \"Training set:\",\n",
        "    \"\\n\\tBayes loss: \",\n",
        "    msd(train_prediction_bayes, trainset[!, target]),\n",
        "    \"\\n\\tOLS loss: \",\n",
        "    msd(train_prediction_ols, trainset[!, target]),\n",
        ")\n",
        "\n",
        "println(\n",
        "    \"Test set:\",\n",
        "    \"\\n\\tBayes loss: \",\n",
        "    msd(test_prediction_bayes, testset[!, target]),\n",
        "    \"\\n\\tOLS loss: \",\n",
        "    msd(test_prediction_ols, testset[!, target]),\n",
        ")\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "let\n",
        "    bayes_train_loss = msd(train_prediction_bayes, trainset[!, target])\n",
        "    bayes_test_loss = msd(test_prediction_bayes, testset[!, target])\n",
        "    ols_train_loss = msd(train_prediction_ols, trainset[!, target])\n",
        "    ols_test_loss = msd(test_prediction_ols, testset[!, target])\n",
        "    @assert bayes_train_loss < bayes_test_loss \"Bayesian training loss ($bayes_train_loss) >= Bayesian test loss ($bayes_test_loss)\"\n",
        "    @assert ols_train_loss < ols_test_loss \"OLS training loss ($ols_train_loss) >= OLS test loss ($ols_test_loss)\"\n",
        "    @assert isapprox(bayes_train_loss, ols_train_loss; rtol=0.01) \"Difference between Bayesian training loss ($bayes_train_loss) and OLS training loss ($ols_train_loss) unexpectedly large!\"\n",
        "    @assert isapprox(bayes_test_loss, ols_test_loss; rtol=0.05) \"Difference between Bayesian test loss ($bayes_test_loss) and OLS test loss ($ols_test_loss) unexpectedly large!\"\n",
        "end\n",
        "```\n",
        "\n",
        "As we can see above, OLS and our Bayesian model fit our training and test data set about the same."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}