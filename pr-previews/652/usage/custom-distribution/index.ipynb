{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Custom Distributions\n",
        "engine: julia\n",
        "aliases:\n",
        "  - ../../tutorials/usage-custom-distribution/index.html\n",
        "  - ../../tutorials/docs-09-using-turing-advanced/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "`Turing.jl` supports the use of distributions from the Distributions.jl package.\n",
        "By extension, it also supports the use of customized distributions by defining them as subtypes of `Distribution` type of the Distributions.jl package, as well as corresponding functions.\n",
        "\n",
        "This page shows a workflow of how to define a customized distribution, using our own implementation of a simple `Uniform` distribution as a simple example.\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "using Distributions, Turing, Random, Bijectors\n",
        "```\n",
        "\n",
        "## Define the Distribution Type\n",
        "\n",
        "First, define a type of the distribution, as a subtype of a corresponding distribution type in the Distributions.jl package.\n",
        "\n",
        "```{julia}\n",
        "struct CustomUniform <: ContinuousUnivariateDistribution end\n",
        "```\n",
        "\n",
        "## Implement Sampling and Evaluation of the log-pdf\n",
        "\n",
        "Second, implement the `rand` and `logpdf` functions for your new distribution, which will be used to run the model.\n",
        "\n",
        "```{julia}\n",
        "# sample in [0, 1]\n",
        "Distributions.rand(rng::AbstractRNG, d::CustomUniform) = rand(rng)\n",
        "\n",
        "# p(x) = 1 → log[p(x)] = 0\n",
        "Distributions.logpdf(d::CustomUniform, x::Real) = zero(x)\n",
        "```\n",
        "\n",
        "## Define Helper Functions\n",
        "\n",
        "In most cases, it may be required to define some helper functions.\n",
        "\n",
        "### Domain Transformation\n",
        "\n",
        "Certain samplers, such as `HMC`, require the domain of the priors to be unbounded.\n",
        "Therefore, to use our `CustomUniform` as a prior in a model we also need to define how to transform samples from `[0, 1]` to `ℝ`.\n",
        "To do this, we need to define the corresponding `Bijector` from `Bijectors.jl`, which is what `Turing.jl` uses internally to deal with constrained distributions.\n",
        "\n",
        "To transform from `[0, 1]` to `ℝ` we can use the `Logit` bijector:\n",
        "\n",
        "```{julia}\n",
        "Bijectors.bijector(d::CustomUniform) = Logit(0.0, 1.0)\n",
        "```\n",
        "\n",
        "In the present example, `CustomUniform` is a subtype of `ContinuousUnivariateDistribution`.\n",
        "The procedure for subtypes of `ContinuousMultivariateDistribution` and `ContinuousMatrixDistribution` is exactly the same.\n",
        "For example, `Wishart` defines a distribution over positive-definite matrices and so `bijector` returns a `PDBijector` when called with a `Wishart` distribution as an argument.\n",
        "For discrete distributions, there is no need to define a bijector; the `Identity` bijector is used by default.\n",
        "\n",
        "As an alternative to the above, for `UnivariateDistribution` we could define the `minimum` and `maximum` of the distribution:\n",
        "\n",
        "```{julia}\n",
        "Distributions.minimum(d::CustomUniform) = 0.0\n",
        "Distributions.maximum(d::CustomUniform) = 1.0\n",
        "```\n",
        "\n",
        "and `Bijectors.jl` will return a default `Bijector` called `TruncatedBijector` which makes use of `minimum` and `maximum` derive the correct transformation.\n",
        "\n",
        "Internally, Turing basically does the following when it needs to convert a constrained distribution to an unconstrained distribution, e.g. when sampling using `HMC`:\n",
        "\n",
        "```{julia}\n",
        "dist = Gamma(2,3)\n",
        "b = bijector(dist)\n",
        "transformed_dist = transformed(dist, b) # results in distribution with transformed support + correction for logpdf\n",
        "```\n",
        "\n",
        "and then we can call `rand` and `logpdf` as usual, where\n",
        "\n",
        "  - `rand(transformed_dist)` returns a sample in the unconstrained space, and\n",
        "  - `logpdf(transformed_dist, y)` returns the log density of the original distribution, but with `y` living in the unconstrained space.\n",
        "\n",
        "To read more about Bijectors.jl, check out [its documentation](https://turinglang.org/Bijectors.jl/stable/)."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}