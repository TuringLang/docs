{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Hidden Markov Models\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../04-hidden-markov-model/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "This tutorial illustrates training Bayesian [hidden Markov models](https://en.wikipedia.org/wiki/Hidden_Markov_model) (HMMs) using Turing.\n",
        "The main goals are learning the transition matrix, emission parameter, and hidden states.\n",
        "For a more rigorous academic overview of hidden Markov models, see [An Introduction to Hidden Markov Models and Bayesian Networks](https://mlg.eng.cam.ac.uk/zoubin/papers/ijprai.pdf) (Ghahramani, 2001).\n",
        "\n",
        "In this tutorial, we assume there are $k$ discrete hidden states; the observations are continuous and normally distributed - centered around the hidden states. This assumption reduces the number of parameters to be estimated in the emission matrix.\n",
        "\n",
        "Let's load the libraries we'll need, and set a random seed for reproducibility.\n",
        "\n",
        "```{julia}\n",
        "# Load libraries.\n",
        "using Turing, StatsPlots, Random, Bijectors\n",
        "\n",
        "# Set a random seed\n",
        "Random.seed!(12345678);\n",
        "```\n",
        "\n",
        "## Simple State Detection\n",
        "\n",
        "In this example, we'll use something where the states and emission parameters are straightforward.\n",
        "\n",
        "```{julia}\n",
        "# Define the emission parameter.\n",
        "y = [fill(1.0, 6)..., fill(2.0, 6)..., fill(3.0, 7)...,\n",
        "  fill(2.0, 4)..., fill(1.0, 7)...]\n",
        "N = length(y);\n",
        "K = 3;\n",
        "\n",
        "# Plot the data we just made.\n",
        "plot(y; xlim=(0, 30), ylim=(-1, 5), size=(500, 250), legend = false)\n",
        "scatter!(y, color = :blue; xlim=(0, 30), ylim=(-1, 5), size=(500, 250), legend = false)\n",
        "```\n",
        "\n",
        "We can see that we have three states, one for each height of the plot (1, 2, 3). This height is also our emission parameter, so state one produces a value of one, state two produces a value of two, and so on.\n",
        "\n",
        "Ultimately, we would like to understand three major parameters:\n",
        "\n",
        " 1. The transition matrix. This is a matrix that assigns a probability of switching from one state to any other state, including the state that we are already in.\n",
        " 2. The emission parameters, which describes a typical value emitted by some state. In the plot above, the emission parameter for state one is simply one.\n",
        " 3. The state sequence is our understanding of what state we were actually in when we observed some data. This is very important in more sophisticated HMMs, where the emission value does not equal our state.\n",
        "\n",
        "With this in mind, let's set up our model. We are going to use some of our knowledge as modelers to provide additional information about our system. This takes the form of the prior on our emission parameter.\n",
        "\n",
        "$$\n",
        "m_i \\sim \\mathrm{Normal}(i, 0.5) \\quad \\text{where} \\quad m = \\{1,2,3\\}\n",
        "$$\n",
        "\n",
        "Simply put, this says that we expect state one to emit values in a Normally distributed manner, where the mean of each state's emissions is that state's value. The variance of 0.5 helps the model converge more quickly â€” consider the case where we have a variance of 1 or 2. In this case, the likelihood of observing a 2 when we are in state 1 is actually quite high, as it is within a standard deviation of the true emission value. Applying the prior that we are likely to be tightly centered around the mean prevents our model from being too confused about the state that is generating our observations.\n",
        "\n",
        "The priors on our transition matrix are noninformative, using `T[i] ~ Dirichlet(ones(K)/K)`. The Dirichlet prior used in this way assumes that the state is likely to change to any other state with equal probability. As we'll see, this transition matrix prior will be overwritten as we observe data.\n",
        "\n",
        "```{julia}\n",
        "# Turing model definition.\n",
        "@model function BayesHmm(y, K)\n",
        "    # Get observation length.\n",
        "    N = length(y)\n",
        "\n",
        "    # State sequence.\n",
        "    s = zeros(Int, N)\n",
        "\n",
        "    # Emission matrix.\n",
        "    m = Vector(undef, K)\n",
        "\n",
        "    # Transition matrix.\n",
        "    T = Vector{Vector}(undef, K)\n",
        "\n",
        "    # Assign distributions to each element\n",
        "    # of the transition matrix and the\n",
        "    # emission matrix.\n",
        "    for i in 1:K\n",
        "        T[i] ~ Dirichlet(ones(K) / K)\n",
        "        m[i] ~ Normal(i, 0.5)\n",
        "    end\n",
        "\n",
        "    # Observe each point of the input.\n",
        "    s[1] ~ Categorical(K)\n",
        "    y[1] ~ Normal(m[s[1]], 0.1)\n",
        "\n",
        "    for i in 2:N\n",
        "        s[i] ~ Categorical(vec(T[s[i - 1]]))\n",
        "        y[i] ~ Normal(m[s[i]], 0.1)\n",
        "    end\n",
        "end;\n",
        "```\n",
        "\n",
        "We will use a combination of two samplers (HMC and Particle Gibbs) by passing them to the Gibbs sampler. The Gibbs sampler allows for compositional inference, where we can utilize different samplers on different parameters. (For API details of these samplers, please see [Turing.jl's API documentation](https://turinglang.org/Turing.jl/stable/api/Inference/).)\n",
        "\n",
        "In this case, we use HMC for `m` and `T`, representing the emission and transition matrices respectively. We use the Particle Gibbs sampler for `s`, the state sequence. You may wonder why it is that we are not assigning `s` to the HMC sampler, and why it is that we need compositional Gibbs sampling at all.\n",
        "\n",
        "The parameter `s` is not a continuous variable.\n",
        "It is a vector of **integers**, and thus Hamiltonian methods like HMC and NUTS won't work correctly.\n",
        "Gibbs allows us to apply the right tools to the best effect.\n",
        "If you are a particularly advanced user interested in higher performance, you may benefit from setting up your Gibbs sampler to use [different automatic differentiation]({{<meta usage-automatic-differentiation>}}#compositional-sampling-with-differing-ad-modes) backends for each parameter space.\n",
        "\n",
        "Time to run our sampler.\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "#| echo: false\n",
        "setprogress!(false)\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "g = Gibbs((:m, :T) => HMC(0.01, 50), :s => PG(120))\n",
        "chn = sample(BayesHmm(y, 3), g, 1000)\n",
        "```\n",
        "\n",
        "Let's see how well our chain performed.\n",
        "Ordinarily, using `display(chn)` would be a good first step, but we have generated a lot of parameters here (`s[1]`, `s[2]`, `m[1]`, and so on).\n",
        "It's a bit easier to show how our model performed graphically.\n",
        "\n",
        "The code below generates an animation showing the graph of the data above, and the data our model generates in each sample.\n",
        "\n",
        "```{julia}\n",
        "# Extract our m and s parameters from the chain.\n",
        "m_set = MCMCChains.group(chn, :m).value\n",
        "s_set = MCMCChains.group(chn, :s).value\n",
        "\n",
        "# Iterate through the MCMC samples.\n",
        "Ns = 1:length(chn)\n",
        "\n",
        "# Make an animation.\n",
        "animation = @gif for i in Ns\n",
        "    m = m_set[i, :]\n",
        "    s = Int.(s_set[i, :])\n",
        "    emissions = m[s]\n",
        "\n",
        "    p = plot(\n",
        "        y;\n",
        "        chn=:red,\n",
        "        size=(500, 250),\n",
        "        xlabel=\"Time\",\n",
        "        ylabel=\"State\",\n",
        "        legend=:topright,\n",
        "        label=\"True data\",\n",
        "        xlim=(0, 30),\n",
        "        ylim=(-1, 5),\n",
        "    )\n",
        "    plot!(emissions; color=:blue, label=\"Sample $i\")\n",
        "end every 3\n",
        "```\n",
        "\n",
        "Looks like our model did a pretty good job, but we should also check to make sure our chain converges. A quick check is to examine whether the diagonal (representing the probability of remaining in the current state) of the transition matrix appears to be stationary. The code below extracts the diagonal and shows a traceplot of each persistence probability.\n",
        "\n",
        "```{julia}\n",
        "# Index the chain with the persistence probabilities.\n",
        "subchain = chn[[\"T[1][1]\", \"T[2][2]\", \"T[3][3]\"]]\n",
        "\n",
        "plot(subchain; seriestype=:traceplot, title=\"Persistence Probability\", legend=false)\n",
        "```\n",
        "\n",
        "A cursory examination of the traceplot above indicates that all three chains converged to something resembling\n",
        "stationary. We can use the diagnostic functions provided by [MCMCChains](https://github.com/TuringLang/MCMCChains.jl) to engage in some more formal tests, like the Heidelberg and Welch diagnostic:\n",
        "\n",
        "```{julia}\n",
        "heideldiag(MCMCChains.group(chn, :T))[1]\n",
        "```\n",
        "\n",
        "The p-values on the test suggest that we cannot reject the hypothesis that the observed sequence comes from a stationary distribution, so we can be reasonably confident that our transition matrix has converged to something reasonable.\n",
        "\n",
        "## Efficient Inference With The Forward Algorithm\n",
        "\n",
        "While the above method works well for the simple example in this tutorial, some users may desire a more efficient method, especially when their model is more complicated.\n",
        "One simple way to improve inference is to marginalize out the hidden states of the model with an appropriate algorithm, calculating only the posterior over the continuous random variables.\n",
        "Not only does this allow more efficient inference via Rao-Blackwellization, but now we can sample our model with `NUTS()` alone, which is usually a much more performant MCMC kernel.\n",
        "\n",
        "Thankfully, [HiddenMarkovModels.jl](https://github.com/gdalle/HiddenMarkovModels.jl) provides an extremely efficient implementation of many algorithms related to hidden Markov models. This allows us to rewrite our model as:\n",
        "\n",
        "```{julia}\n",
        "using HiddenMarkovModels\n",
        "using FillArrays\n",
        "using LinearAlgebra\n",
        "using LogExpFunctions\n",
        "\n",
        "\n",
        "@model function BayesHmm2(y, K)\n",
        "    m ~ Bijectors.ordered(MvNormal([1.0, 2.0, 3.0], 0.5I))\n",
        "    T ~ filldist(Dirichlet(fill(1/K, K)), K)\n",
        "\n",
        "    hmm = HMM(softmax(ones(K)), copy(T'), [Normal(m[i], 0.1) for i in 1:K])\n",
        "    @addlogprob! logdensityof(hmm, y)\n",
        "end\n",
        "\n",
        "chn2 = sample(BayesHmm2(y, 3), NUTS(), 1000)\n",
        "```\n",
        "\n",
        "\n",
        "We can compare the chains of these two models, confirming the posterior estimate is similar (modulo label switching concerns with the Gibbs model):\n",
        "```{julia}\n",
        "#| code-fold: true\n",
        "#| code-summary: \"Plotting Chains\"\n",
        "\n",
        "plot(chn[\"m[1]\"], label = \"m[1], Model 1, Gibbs\", color = :lightblue)\n",
        "plot!(chn2[\"m[1]\"], label = \"m[1], Model 2, NUTS\", color = :blue)\n",
        "plot!(chn[\"m[2]\"], label = \"m[2], Model 1, Gibbs\", color = :pink)\n",
        "plot!(chn2[\"m[2]\"], label = \"m[2], Model 2, NUTS\", color = :red)\n",
        "plot!(chn[\"m[3]\"], label = \"m[3], Model 1, Gibbs\", color = :yellow)\n",
        "plot!(chn2[\"m[3]\"], label = \"m[3], Model 2, NUTS\", color = :orange)\n",
        "```\n",
        "\n",
        "\n",
        "### Recovering Marginalized Trajectories\n",
        "\n",
        "We can use the `viterbi()` algorithm, also from the `HiddenMarkovModels` package, to recover the most probable state for each parameter set in our posterior sample:\n",
        "```{julia}\n",
        "@model function BayesHmmRecover(y, K, IncludeGenerated = false)\n",
        "    m ~ Bijectors.ordered(MvNormal([1.0, 2.0, 3.0], 0.5I))\n",
        "    T ~ filldist(Dirichlet(fill(1/K, K)), K)\n",
        "\n",
        "    hmm = HMM(softmax(ones(K)), copy(T'), [Normal(m[i], 0.1) for i in 1:K])\n",
        "    @addlogprob! logdensityof(hmm, y)\n",
        "\n",
        "    # Conditional generation of the hidden states.\n",
        "    if IncludeGenerated\n",
        "        seq, _ = viterbi(hmm, y)\n",
        "        s := [m[s] for s in seq]\n",
        "    end\n",
        "end\n",
        "\n",
        "chn_recover = sample(BayesHmmRecover(y, 3, true), NUTS(), 1000)\n",
        "```\n",
        "\n",
        "Plotting the estimated states, we can see that the results align well with our expectations:\n",
        "\n",
        "```{julia}\n",
        "p = plot(xlim=(0, 30), ylim=(-1, 5), size=(500, 250))\n",
        "for i in 1:100\n",
        "    ind = rand(DiscreteUniform(1, 1000))\n",
        "    plot!(MCMCChains.group(chn_recover, :s).value[ind,:], color = :grey, opacity = 0.1, legend = :false)\n",
        "end\n",
        "scatter!(y, color = :blue)\n",
        "\n",
        "p\n",
        "```"
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}