{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Manually Defining a Model\n",
        "engine: julia\n",
        "aliases:\n",
        "  - ../../../tutorials/dev-model-manual/index.html\n",
        "---\n",
        "\n",
        "Traditionally, models in Turing are defined using the `@model` macro:\n",
        "\n",
        "```{julia}\n",
        "using Turing\n",
        "\n",
        "@model function gdemo(x)\n",
        "    # Set priors.\n",
        "    s² ~ InverseGamma(2, 3)\n",
        "    m ~ Normal(0, sqrt(s²))\n",
        "\n",
        "    # Observe each value of x.\n",
        "    x .~ Normal(m, sqrt(s²))\n",
        "\n",
        "    return nothing\n",
        "end\n",
        "\n",
        "model = gdemo([1.5, 2.0])\n",
        "```\n",
        "\n",
        "The `@model` macro accepts a function definition and rewrites it such that call of the function generates a `Model` struct for use by the sampler.\n",
        "\n",
        "However, models can be constructed by hand without the use of a macro.\n",
        "Taking the `gdemo` model above as an example, the macro-based definition can be implemented also (a bit less generally) with the macro-free version\n",
        "\n",
        "```{julia}\n",
        "using DynamicPPL\n",
        "\n",
        "# Create the model function.\n",
        "function gdemo2(model, varinfo, x)\n",
        "    # Assume s² has an InverseGamma distribution.\n",
        "    s², varinfo = DynamicPPL.tilde_assume!!(\n",
        "        model.context, InverseGamma(2, 3), @varname(s²), varinfo\n",
        "    )\n",
        "\n",
        "    # Assume m has a Normal distribution.\n",
        "    m, varinfo = DynamicPPL.tilde_assume!!(\n",
        "        model.context, Normal(0, sqrt(s²)), @varname(m), varinfo\n",
        "    )\n",
        "\n",
        "    # Observe each value of x[i] according to a Normal distribution.\n",
        "    for i in eachindex(x)\n",
        "        _retval, varinfo = DynamicPPL.tilde_observe!!(\n",
        "            model.context, Normal(m, sqrt(s²)), x[i], @varname(x[i]), varinfo\n",
        "        )\n",
        "    end\n",
        "\n",
        "    # The final return statement should comprise both the original return\n",
        "    # value and the updated varinfo.\n",
        "    return nothing, varinfo\n",
        "end\n",
        "gdemo2(x) = DynamicPPL.Model(gdemo2, (; x))\n",
        "\n",
        "# Instantiate a Model object with our data variables.\n",
        "model2 = gdemo2([1.5, 2.0])\n",
        "```\n",
        "\n",
        "We can sample from this model in the same way:\n",
        "\n",
        "```{julia}\n",
        "chain = sample(model2, NUTS(), 1000; progress=false)\n",
        "```\n",
        "\n",
        "The subsequent pages in this section will show how the `@model` macro does this behind-the-scenes."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}