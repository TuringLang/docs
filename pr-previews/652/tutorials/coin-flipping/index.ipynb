{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: \"Introduction: Coin Flipping\"\n",
        "engine: julia\n",
        "aliases:\n",
        "  - ../00-introduction/index.html\n",
        "  - ../00-introduction/\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "This is the first of a series of guided tutorials on the Turing language.\n",
        "In this tutorial, we will use Bayesian inference to estimate the probability that a coin flip will result in heads, given a series of observations.\n",
        "\n",
        "### Setup\n",
        "\n",
        "First, let us load some packages that we need to simulate a coin flip:\n",
        "\n",
        "```{julia}\n",
        "using Distributions\n",
        "\n",
        "using Random\n",
        "Random.seed!(12); # Set seed for reproducibility\n",
        "```\n",
        "\n",
        "and to visualize our results.\n",
        "\n",
        "```{julia}\n",
        "using StatsPlots\n",
        "```\n",
        "\n",
        "Note that Turing is not loaded here — we do not use it in this example.\n",
        "Next, we configure the data generating model. Let us set the true probability that a coin flip turns up heads\n",
        "\n",
        "```{julia}\n",
        "p_true = 0.5;\n",
        "```\n",
        "\n",
        "and set the number of coin flips we will show our model.\n",
        "\n",
        "```{julia}\n",
        "N = 100;\n",
        "```\n",
        "\n",
        "We simulate `N` coin flips by drawing N random samples from the Bernoulli distribution with success probability `p_true`. The draws are collected in a variable called `data`:\n",
        "\n",
        "```{julia}\n",
        "data = rand(Bernoulli(p_true), N);\n",
        "```\n",
        "\n",
        "Here are the first five coin flips:\n",
        "\n",
        "```{julia}\n",
        "data[1:5]\n",
        "```\n",
        "\n",
        "\n",
        "### Coin Flipping Without Turing\n",
        "\n",
        "The following example illustrates the effect of updating our beliefs with every piece of new evidence we observe.\n",
        "\n",
        "Assume that we are unsure about the probability of heads in a coin flip. To get an intuitive understanding of what \"updating our beliefs\" is, we will visualize the probability of heads in a coin flip after each observed evidence.\n",
        "\n",
        "We begin by specifying a prior belief about the distribution of heads and tails in a coin toss. Here we choose a [Beta](https://en.wikipedia.org/wiki/Beta_distribution) distribution as prior distribution for the probability of heads. Before any coin flip is observed, we assume a uniform distribution $\\operatorname{U}(0, 1) = \\operatorname{Beta}(1, 1)$ of the probability of heads. I.e., every probability is equally likely initially.\n",
        "\n",
        "```{julia}\n",
        "prior_belief = Beta(1, 1);\n",
        "```\n",
        "\n",
        "With our priors set and our data at hand, we can perform Bayesian inference.\n",
        "\n",
        "This is a fairly simple process. We expose one additional coin flip to our model every iteration, such that the first run only sees the first coin flip, while the last iteration sees all the coin flips. In each iteration we update our belief to an updated version of the original Beta distribution that accounts for the new proportion of heads and tails. The update is particularly simple since our prior distribution is a [conjugate prior](https://en.wikipedia.org/wiki/Conjugate_prior). Note that a closed-form expression for the posterior (implemented in the `updated_belief` expression below) is not accessible in general and usually does not exist for more interesting models.\n",
        "\n",
        "```{julia}\n",
        "function updated_belief(prior_belief::Beta, data::AbstractArray{Bool})\n",
        "    # Count the number of heads and tails.\n",
        "    heads = sum(data)\n",
        "    tails = length(data) - heads\n",
        "\n",
        "    # Update our prior belief in closed form (this is possible because we use a conjugate prior).\n",
        "    return Beta(prior_belief.α + heads, prior_belief.β + tails)\n",
        "end\n",
        "\n",
        "# Show updated belief for increasing number of observations\n",
        "@gif for n in 0:N\n",
        "    plot(\n",
        "        updated_belief(prior_belief, data[1:n]);\n",
        "        size=(500, 250),\n",
        "        title=\"Updated belief after $n observations\",\n",
        "        xlabel=\"probability of heads\",\n",
        "        ylabel=\"\",\n",
        "        legend=nothing,\n",
        "        xlim=(0, 1),\n",
        "        fill=0,\n",
        "        α=0.3,\n",
        "        w=3,\n",
        "    )\n",
        "    vline!([p_true])\n",
        "end\n",
        "```\n",
        "\n",
        "The animation above shows that with increasing evidence our belief about the probability of heads in a coin flip slowly adjusts towards the true value.\n",
        "The orange line in the animation represents the true probability of seeing heads on a single coin flip, while the mode of the distribution shows what the model believes the probability of a heads is given the evidence it has seen.\n",
        "\n",
        "For the mathematically inclined, the $\\operatorname{Beta}$ distribution is updated by adding each coin flip to the parameters $\\alpha$ and $\\beta$ of the distribution.\n",
        "Initially, the parameters are defined as $\\alpha = 1$ and $\\beta = 1$.\n",
        "Over time, with more and more coin flips, $\\alpha$ and $\\beta$ will be approximately equal to each other as we are equally likely to flip a heads or a tails.\n",
        "\n",
        "The mean of the $\\operatorname{Beta}(\\alpha, \\beta)$ distribution is\n",
        "\n",
        "$$\\operatorname{E}[X] = \\dfrac{\\alpha}{\\alpha+\\beta}.$$\n",
        "\n",
        "This implies that the plot of the distribution will become centered around 0.5 for a large enough number of coin flips, as we expect $\\alpha \\approx \\beta$.\n",
        "\n",
        "The variance of the $\\operatorname{Beta}(\\alpha, \\beta)$ distribution is\n",
        "\n",
        "$$\\operatorname{var}[X] = \\dfrac{\\alpha\\beta}{(\\alpha + \\beta)^2 (\\alpha + \\beta + 1)}.$$\n",
        "\n",
        "Thus the variance of the distribution will approach 0 with more and more samples, as the denominator will grow faster than will the numerator.\n",
        "More samples means less variance.\n",
        "This implies that the distribution will reflect less uncertainty about the probability of receiving a heads and the plot will become more tightly centered around 0.5 for a large enough number of coin flips.\n",
        "\n",
        "### Coin Flipping With Turing\n",
        "\n",
        "We now move away from the closed-form expression above.\n",
        "We use **Turing** to specify the same model and to approximate the posterior distribution with samples.\n",
        "To do so, we first need to load `Turing`.\n",
        "\n",
        "```{julia}\n",
        "using Turing\n",
        "```\n",
        "\n",
        "Additionally, we load `MCMCChains`, a library for analyzing and visualizing the samples with which we approximate the posterior distribution.\n",
        "\n",
        "```{julia}\n",
        "using MCMCChains\n",
        "```\n",
        "\n",
        "First, we define the coin-flip model using Turing.\n",
        "\n",
        "```{julia}\n",
        "# Unconditioned coinflip model with `N` observations.\n",
        "@model function coinflip(; N::Int)\n",
        "    # Our prior belief about the probability of heads in a coin toss.\n",
        "    p ~ Beta(1, 1)\n",
        "\n",
        "    # Heads or tails of a coin are drawn from `N` independent and identically\n",
        "    # distributed Bernoulli distributions with success rate `p`.\n",
        "    y ~ filldist(Bernoulli(p), N)\n",
        "\n",
        "    return y\n",
        "end;\n",
        "```\n",
        "\n",
        "In the Turing model the prior distribution of the variable `p`, the probability of heads in a coin toss, and the distribution of the observations `y` are specified on the right-hand side of the `~` expressions.\n",
        "The `@model` macro modifies the body of the Julia function `coinflip` and, e.g., replaces the `~` statements with internal function calls that are used for sampling.\n",
        "\n",
        "Here we defined a model that is not conditioned on any specific observations as this allows us to easily obtain samples of both `p` and `y` with\n",
        "\n",
        "```{julia}\n",
        "rand(coinflip(; N))\n",
        "```\n",
        "\n",
        "The model can be conditioned on some observations with `|`.\n",
        "See the [documentation of the `condition` syntax](https://turinglang.github.io/DynamicPPL.jl/stable/api/#Condition-and-decondition) in `DynamicPPL.jl` for more details.\n",
        "In the conditioned `model` the observations `y` are fixed to `data`.\n",
        "\n",
        "```{julia}\n",
        "coinflip(y::AbstractVector{<:Real}) = coinflip(; N=length(y)) | (; y)\n",
        "\n",
        "model = coinflip(data);\n",
        "```\n",
        "\n",
        "After defining the model, we can approximate the posterior distribution by drawing samples from the distribution.\n",
        "In this example, we use a [Hamiltonian Monte Carlo](https://en.wikipedia.org/wiki/Hamiltonian_Monte_Carlo) sampler to draw these samples.\n",
        "Other tutorials give more information on the samplers available in Turing and discuss their use for different models.\n",
        "\n",
        "```{julia}\n",
        "sampler = NUTS();\n",
        "```\n",
        "\n",
        "We approximate the posterior distribution with 1000 samples:\n",
        "\n",
        "```{julia}\n",
        "chain = sample(model, sampler, 2_000, progress=false);\n",
        "```\n",
        "\n",
        "The `sample` function and common keyword arguments are explained more extensively in the documentation of [AbstractMCMC.jl](https://turinglang.github.io/AbstractMCMC.jl/dev/api/).\n",
        "\n",
        "After finishing the sampling process, we can visually compare the closed-form posterior distribution with the approximation obtained with Turing.\n",
        "\n",
        "```{julia}\n",
        "histogram(chain)\n",
        "```\n",
        "\n",
        "Now we can build our plot:\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "@assert isapprox(mean(chain, :p), 0.5; atol=0.1) \"Estimated mean of parameter p: $(mean(chain, :p)) - not in [0.4, 0.6]!\"\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "# Visualize a blue density plot of the approximate posterior distribution using HMC (see Chain 1 in the legend).\n",
        "density(chain; xlim=(0, 1), legend=:best, w=2, c=:blue)\n",
        "\n",
        "# Visualize a green density plot of the posterior distribution in closed-form.\n",
        "plot!(\n",
        "    0:0.01:1,\n",
        "    pdf.(updated_belief(prior_belief, data), 0:0.01:1);\n",
        "    xlabel=\"probability of heads\",\n",
        "    ylabel=\"\",\n",
        "    title=\"\",\n",
        "    xlim=(0, 1),\n",
        "    label=\"Closed-form\",\n",
        "    fill=0,\n",
        "    α=0.3,\n",
        "    w=3,\n",
        "    c=:lightgreen,\n",
        ")\n",
        "\n",
        "# Visualize the true probability of heads in red.\n",
        "vline!([p_true]; label=\"True probability\", c=:red)\n",
        "```\n",
        "\n",
        "As we can see, the samples obtained with Turing closely approximate the true posterior distribution.\n",
        "Hopefully this tutorial has provided an easy-to-follow, yet informative introduction to Turing's simpler applications.\n",
        "More advanced usage is demonstrated in other tutorials."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}