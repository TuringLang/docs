{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Gaussian Process Latent Variable Models\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../12-gplvm/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "In a previous tutorial, we have discussed latent variable models, in particular probabilistic principal component analysis (pPCA).\n",
        "Here, we show how we can extend the mapping provided by pPCA to non-linear mappings between input and output.\n",
        "For more details about the Gaussian Process Latent Variable Model (GPLVM),\n",
        "we refer the reader to the [original publication](https://jmlr.org/papers/v6/lawrence05a.html) and a [further extension](http://proceedings.mlr.press/v9/titsias10a/titsias10a.pdf).\n",
        "\n",
        "In short, the GPVLM is a dimensionality reduction technique that allows us to embed a high-dimensional dataset in a lower-dimensional embedding.\n",
        "Importantly, it provides the advantage that the linear mappings from the embedded space can be non-linearised through the use of Gaussian Processes.\n",
        "\n",
        "### Let's start by loading some dependencies.\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "using Turing\n",
        "using AbstractGPs\n",
        "using FillArrays\n",
        "using LaTeXStrings\n",
        "using Plots\n",
        "using RDatasets\n",
        "using ReverseDiff\n",
        "using StatsBase\n",
        "\n",
        "using LinearAlgebra\n",
        "using Random\n",
        "\n",
        "Random.seed!(1789);\n",
        "```\n",
        "\n",
        "We demonstrate the GPLVM with a very small dataset: [Fisher's Iris data set](https://en.wikipedia.org/wiki/Iris_flower_data_set).\n",
        "This is mostly for reasons of run time, so the tutorial can be run quickly.\n",
        "As you will see, one of the major drawbacks of using GPs is their speed,\n",
        "although this is an active area of research.\n",
        "We will briefly touch on some ways to speed things up at the end of this tutorial.\n",
        "We transform the original data with non-linear operations in order to demonstrate the power of GPs to work on non-linear relationships, while keeping the problem reasonably small.\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "data = dataset(\"datasets\", \"iris\")\n",
        "species = data[!, \"Species\"]\n",
        "index = shuffle(1:150)\n",
        "# we extract the four measured quantities,\n",
        "# so the dimension of the data is only d=4 for this toy example\n",
        "dat = Matrix(data[index, 1:4])\n",
        "labels = data[index, \"Species\"]\n",
        "\n",
        "# non-linearize data to demonstrate ability of GPs to deal with non-linearity\n",
        "dat[:, 1] = 0.5 * dat[:, 1] .^ 2 + 0.1 * dat[:, 1] .^ 3\n",
        "dat[:, 2] = dat[:, 2] .^ 3 + 0.2 * dat[:, 2] .^ 4\n",
        "dat[:, 3] = 0.1 * exp.(dat[:, 3]) - 0.2 * dat[:, 3] .^ 2\n",
        "dat[:, 4] = 0.5 * log.(dat[:, 4]) .^ 2 + 0.01 * dat[:, 3] .^ 5\n",
        "\n",
        "# normalize data\n",
        "dt = fit(ZScoreTransform, dat; dims=1);\n",
        "StatsBase.transform!(dt, dat);\n",
        "```\n",
        "\n",
        "We will start out by demonstrating the basic similarity between pPCA (see the tutorial on this topic) and the GPLVM model.\n",
        "Indeed, pPCA is basically equivalent to running the GPLVM model with an automatic relevance determination (ARD) linear kernel.\n",
        "\n",
        "First, we re-introduce the pPCA model (see the tutorial on pPCA for details)\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "@model function pPCA(x)\n",
        "    # Dimensionality of the problem.\n",
        "    N, D = size(x)\n",
        "    # latent variable z\n",
        "    z ~ filldist(Normal(), D, N)\n",
        "    # weights/loadings W\n",
        "    w ~ filldist(Normal(), D, D)\n",
        "    mu = (w * z)'\n",
        "    for d in 1:D\n",
        "        x[:, d] ~ MvNormal(mu[:, d], I)\n",
        "    end\n",
        "    return nothing\n",
        "end;\n",
        "```\n",
        "\n",
        "We define two different kernels, a simple linear kernel with an Automatic Relevance Determination transform and a\n",
        "squared exponential kernel.\n",
        "\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "linear_kernel(α) = LinearKernel() ∘ ARDTransform(α)\n",
        "sekernel(α, σ) = σ * SqExponentialKernel() ∘ ARDTransform(α);\n",
        "```\n",
        "\n",
        "And here is the GPLVM model.\n",
        "We create separate models for the two types of kernel.\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "@model function GPLVM_linear(Y, K)\n",
        "    # Dimensionality of the problem.\n",
        "    N, D = size(Y)\n",
        "    # K is the dimension of the latent space\n",
        "    @assert K <= D\n",
        "    noise = 1e-3\n",
        "\n",
        "    # Priors\n",
        "    α ~ MvLogNormal(MvNormal(Zeros(K), I))\n",
        "    Z ~ filldist(Normal(), K, N)\n",
        "    mu ~ filldist(Normal(), N)\n",
        "\n",
        "    gp = GP(linear_kernel(α))\n",
        "    gpz = gp(ColVecs(Z), noise)\n",
        "    Y ~ filldist(MvNormal(mu, cov(gpz)), D)\n",
        "\n",
        "    return nothing\n",
        "end;\n",
        "\n",
        "@model function GPLVM(Y, K)\n",
        "    # Dimensionality of the problem.\n",
        "    N, D = size(Y)\n",
        "    # K is the dimension of the latent space\n",
        "    @assert K <= D\n",
        "    noise = 1e-3\n",
        "\n",
        "    # Priors\n",
        "    α ~ MvLogNormal(MvNormal(Zeros(K), I))\n",
        "    σ ~ LogNormal(0.0, 1.0)\n",
        "    Z ~ filldist(Normal(), K, N)\n",
        "    mu ~ filldist(Normal(), N)\n",
        "\n",
        "    gp = GP(sekernel(α, σ))\n",
        "    gpz = gp(ColVecs(Z), noise)\n",
        "    Y ~ filldist(MvNormal(mu, cov(gpz)), D)\n",
        "\n",
        "    return nothing\n",
        "end;\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "# Standard GPs don't scale very well in n, so we use a small subsample for the purpose of this tutorial\n",
        "n_data = 40\n",
        "# number of features to use from dataset\n",
        "n_features = 4\n",
        "# latent dimension for GP case\n",
        "ndim = 4;\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "ppca = pPCA(dat[1:n_data, 1:n_features])\n",
        "chain_ppca = sample(ppca, NUTS{Turing.ReverseDiffAD{true}}(), 1000);\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "# we extract the posterior mean estimates of the parameters from the chain\n",
        "z_mean = reshape(mean(group(chain_ppca, :z))[:, 2], (n_features, n_data))\n",
        "scatter(z_mean[1, :], z_mean[2, :]; group=labels[1:n_data], xlabel=L\"z_1\", ylabel=L\"z_2\")\n",
        "```\n",
        "\n",
        "We can see that the pPCA fails to distinguish the groups.\n",
        "In particular, the `setosa` species is not clearly separated from `versicolor` and `virginica`.\n",
        "This is due to the non-linearities that we introduced, as without them the two groups can be clearly distinguished\n",
        "using pPCA (see the pPCA tutorial).\n",
        "\n",
        "Let's try the same with our linear kernel GPLVM model.\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "gplvm_linear = GPLVM_linear(dat[1:n_data, 1:n_features], ndim)\n",
        "chain_linear = sample(gplvm_linear, NUTS{Turing.ReverseDiffAD{true}}(), 500);\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "# we extract the posterior mean estimates of the parameters from the chain\n",
        "z_mean = reshape(mean(group(chain_linear, :Z))[:, 2], (n_features, n_data))\n",
        "alpha_mean = mean(group(chain_linear, :α))[:, 2]\n",
        "\n",
        "alpha1, alpha2 = partialsortperm(alpha_mean, 1:2; rev=true)\n",
        "scatter(\n",
        "    z_mean[alpha1, :],\n",
        "    z_mean[alpha2, :];\n",
        "    group=labels[1:n_data],\n",
        "    xlabel=L\"z_{\\mathrm{ard}_1}\",\n",
        "    ylabel=L\"z_{\\mathrm{ard}_2}\",\n",
        ")\n",
        "```\n",
        "\n",
        "We can see that similar to the pPCA case, the linear kernel GPLVM fails to distinguish between the two groups\n",
        "(`setosa` on the one hand, and `virginica` and `verticolor` on the other).\n",
        "\n",
        "Finally, we demonstrate that by changing the kernel to a non-linear function, we are able to separate the data again.\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "gplvm = GPLVM(dat[1:n_data, 1:n_features], ndim)\n",
        "chain_gplvm = sample(gplvm, NUTS{Turing.ReverseDiffAD{true}}(), 500);\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "# we extract the posterior mean estimates of the parameters from the chain\n",
        "z_mean = reshape(mean(group(chain_gplvm, :Z))[:, 2], (ndim, n_data))\n",
        "alpha_mean = mean(group(chain_gplvm, :α))[:, 2]\n",
        "\n",
        "alpha1, alpha2 = partialsortperm(alpha_mean, 1:2; rev=true)\n",
        "scatter(\n",
        "    z_mean[alpha1, :],\n",
        "    z_mean[alpha2, :];\n",
        "    group=labels[1:n_data],\n",
        "    xlabel=L\"z_{\\mathrm{ard}_1}\",\n",
        "    ylabel=L\"z_{\\mathrm{ard}_2}\",\n",
        ")\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "let\n",
        "    @assert abs(\n",
        "        mean(z_mean[alpha1, labels[1:n_data] .== \"setosa\"]) -\n",
        "        mean(z_mean[alpha1, labels[1:n_data] .!= \"setosa\"]),\n",
        "    ) > 1\n",
        "end\n",
        "```\n",
        "\n",
        "Now, the split between the two groups is visible again."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}