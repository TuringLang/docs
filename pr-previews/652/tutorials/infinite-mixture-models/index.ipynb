{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Infinite Mixture Models\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../06-infinite-mixture-model/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "In many applications it is desirable to allow the model to adjust its complexity to the amount of data. Consider for example the task of assigning objects into clusters or groups. This task often involves the specification of the number of groups. However, often times it is not known beforehand how many groups exist. Moreover, in some applictions, e.g. modelling topics in text documents or grouping species, the number of examples per group is heavy tailed. This makes it impossible to predefine the number of groups and requiring the model to form new groups when data points from previously unseen groups are observed.\n",
        "\n",
        "A natural approach for such applications is the use of non-parametric models. This tutorial will introduce how to use the Dirichlet process in a mixture of infinitely many Gaussians using Turing. For further information on Bayesian nonparametrics and the Dirichlet process we refer to the [introduction by Zoubin Ghahramani](http://mlg.eng.cam.ac.uk/pub/pdf/Gha12.pdf) and the book \"Fundamentals of Nonparametric Bayesian Inference\" by Subhashis Ghosal and Aad van der Vaart.\n",
        "\n",
        "```{julia}\n",
        "using Turing\n",
        "```\n",
        "\n",
        "## Mixture Model\n",
        "\n",
        "Before introducing infinite mixture models in Turing, we will briefly review the construction of finite mixture models. Subsequently, we will define how to use the [Chinese restaurant process](https://en.wikipedia.org/wiki/Chinese_restaurant_process) construction of a Dirichlet process for non-parametric clustering.\n",
        "\n",
        "#### Two-Component Model\n",
        "\n",
        "First, consider the simple case of a mixture model with two Gaussian components with fixed covariance.\n",
        "The generative process of such a model can be written as:\n",
        "\n",
        "\\begin{equation*}\n",
        "\\begin{aligned}\n",
        "\\pi_1 &\\sim \\mathrm{Beta}(a, b) \\\\\n",
        "\\pi_2 &= 1-\\pi_1 \\\\\n",
        "\\mu_1 &\\sim \\mathrm{Normal}(\\mu_0, \\Sigma_0) \\\\\n",
        "\\mu_2 &\\sim \\mathrm{Normal}(\\mu_0, \\Sigma_0) \\\\\n",
        "z_i &\\sim \\mathrm{Categorical}(\\pi_1, \\pi_2) \\\\\n",
        "x_i &\\sim \\mathrm{Normal}(\\mu_{z_i}, \\Sigma)\n",
        "\\end{aligned}\n",
        "\\end{equation*}\n",
        "\n",
        "where $\\pi_1, \\pi_2$ are the mixing weights of the mixture model, i.e. $\\pi_1 + \\pi_2 = 1$, and $z_i$ is a latent assignment of the observation $x_i$ to a component (Gaussian).\n",
        "\n",
        "We can implement this model in Turing for 1D data as follows:\n",
        "\n",
        "```{julia}\n",
        "@model function two_model(x)\n",
        "    # Hyper-parameters\n",
        "    μ0 = 0.0\n",
        "    σ0 = 1.0\n",
        "\n",
        "    # Draw weights.\n",
        "    π1 ~ Beta(1, 1)\n",
        "    π2 = 1 - π1\n",
        "\n",
        "    # Draw locations of the components.\n",
        "    μ1 ~ Normal(μ0, σ0)\n",
        "    μ2 ~ Normal(μ0, σ0)\n",
        "\n",
        "    # Draw latent assignment.\n",
        "    z ~ Categorical([π1, π2])\n",
        "\n",
        "    # Draw observation from selected component.\n",
        "    if z == 1\n",
        "        x ~ Normal(μ1, 1.0)\n",
        "    else\n",
        "        x ~ Normal(μ2, 1.0)\n",
        "    end\n",
        "end\n",
        "```\n",
        "\n",
        "#### Finite Mixture Model\n",
        "\n",
        "If we have more than two components, this model can elegantly be extended using a Dirichlet distribution as prior for the mixing weights $\\pi_1, \\dots, \\pi_K$. Note that the Dirichlet distribution is the multivariate generalization of the beta distribution. The resulting model can be written as:\n",
        "\n",
        "$$\n",
        "\\begin{align}\n",
        "(\\pi_1, \\dots, \\pi_K) &\\sim Dirichlet(K, \\alpha) \\\\\n",
        "\\mu_k &\\sim \\mathrm{Normal}(\\mu_0, \\Sigma_0), \\;\\; \\forall k \\\\\n",
        "z &\\sim Categorical(\\pi_1, \\dots, \\pi_K) \\\\\n",
        "x &\\sim \\mathrm{Normal}(\\mu_z, \\Sigma)\n",
        "\\end{align}\n",
        "$$\n",
        "\n",
        "which resembles the model in the [Gaussian mixture model tutorial]({{<meta gaussian-mixture-model>}}) with a slightly different notation.\n",
        "\n",
        "## Infinite Mixture Model\n",
        "\n",
        "The question now arises, is there a generalization of a Dirichlet distribution for which the dimensionality $K$ is infinite, i.e. $K = \\infty$?\n",
        "\n",
        "But first, to implement an infinite Gaussian mixture model in Turing, we first need to load the `Turing.RandomMeasures` module. `RandomMeasures` contains a variety of tools useful in nonparametrics.\n",
        "\n",
        "```{julia}\n",
        "using Turing.RandomMeasures\n",
        "```\n",
        "\n",
        "We now will utilize the fact that one can integrate out the mixing weights in a Gaussian mixture model allowing us to arrive at the Chinese restaurant process construction. See Carl E. Rasmussen: [The Infinite Gaussian Mixture Model](https://www.seas.harvard.edu/courses/cs281/papers/rasmussen-1999a.pdf), NIPS (2000) for details.\n",
        "\n",
        "In fact, if the mixing weights are integrated out, the conditional prior for the latent variable $z$ is given by:\n",
        "\n",
        "$$\n",
        "p(z_i = k \\mid z_{\\not i}, \\alpha) = \\frac{n_k + \\alpha K}{N - 1 + \\alpha}\n",
        "$$\n",
        "\n",
        "where $z_{\\not i}$ are the latent assignments of all observations except observation $i$. Note that we use $n_k$ to denote the number of observations at component $k$ excluding observation $i$. The parameter $\\alpha$ is the concentration parameter of the Dirichlet distribution used as prior over the mixing weights.\n",
        "\n",
        "#### Chinese Restaurant Process\n",
        "\n",
        "To obtain the Chinese restaurant process construction, we can now derive the conditional prior if $K \\rightarrow \\infty$.\n",
        "\n",
        "For $n_k > 0$ we obtain:\n",
        "\n",
        "$$\n",
        "p(z_i = k \\mid z_{\\not i}, \\alpha) = \\frac{n_k}{N - 1 + \\alpha}\n",
        "$$\n",
        "\n",
        "and for all infinitely many clusters that are empty (combined) we get:\n",
        "\n",
        "$$\n",
        "p(z_i = k \\mid z_{\\not i}, \\alpha) = \\frac{\\alpha}{N - 1 + \\alpha}\n",
        "$$\n",
        "\n",
        "Those equations show that the conditional prior for component assignments is proportional to the number of such observations, meaning that the Chinese restaurant process has a rich get richer property.\n",
        "\n",
        "To get a better understanding of this property, we can plot the cluster choosen by for each new observation drawn from the conditional prior.\n",
        "\n",
        "```{julia}\n",
        "# Concentration parameter.\n",
        "α = 10.0\n",
        "\n",
        "# Random measure, e.g. Dirichlet process.\n",
        "rpm = DirichletProcess(α)\n",
        "\n",
        "# Cluster assignments for each observation.\n",
        "z = Vector{Int}()\n",
        "\n",
        "# Maximum number of observations we observe.\n",
        "Nmax = 500\n",
        "\n",
        "for i in 1:Nmax\n",
        "    # Number of observations per cluster.\n",
        "    K = isempty(z) ? 0 : maximum(z)\n",
        "    nk = Vector{Int}(map(k -> sum(z .== k), 1:K))\n",
        "\n",
        "    # Draw new assignment.\n",
        "    push!(z, rand(ChineseRestaurantProcess(rpm, nk)))\n",
        "end\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "using Plots\n",
        "\n",
        "# Plot the cluster assignments over time\n",
        "@gif for i in 1:Nmax\n",
        "    scatter(\n",
        "        collect(1:i),\n",
        "        z[1:i];\n",
        "        markersize=2,\n",
        "        xlabel=\"observation (i)\",\n",
        "        ylabel=\"cluster (k)\",\n",
        "        legend=false,\n",
        "    )\n",
        "end\n",
        "```\n",
        "\n",
        "Further, we can see that the number of clusters is logarithmic in the number of observations and data points. This is a side-effect of the \"rich-get-richer\" phenomenon, i.e. we expect large clusters and thus the number of clusters has to be smaller than the number of observations.\n",
        "\n",
        "$$\n",
        "\\mathbb{E}[K \\mid N] \\approx \\alpha \\cdot log \\big(1 + \\frac{N}{\\alpha}\\big)\n",
        "$$\n",
        "\n",
        "We can see from the equation that the concentration parameter $\\alpha$ allows us to control the number of clusters formed *a priori*.\n",
        "\n",
        "In Turing we can implement an infinite Gaussian mixture model using the Chinese restaurant process construction of a Dirichlet process as follows:\n",
        "\n",
        "```{julia}\n",
        "@model function infiniteGMM(x)\n",
        "    # Hyper-parameters, i.e. concentration parameter and parameters of H.\n",
        "    α = 1.0\n",
        "    μ0 = 0.0\n",
        "    σ0 = 1.0\n",
        "\n",
        "    # Define random measure, e.g. Dirichlet process.\n",
        "    rpm = DirichletProcess(α)\n",
        "\n",
        "    # Define the base distribution, i.e. expected value of the Dirichlet process.\n",
        "    H = Normal(μ0, σ0)\n",
        "\n",
        "    # Latent assignment.\n",
        "    z = zeros(Int, length(x))\n",
        "\n",
        "    # Locations of the infinitely many clusters.\n",
        "    μ = zeros(Float64, 0)\n",
        "\n",
        "    for i in 1:length(x)\n",
        "\n",
        "        # Number of clusters.\n",
        "        K = maximum(z)\n",
        "        nk = Vector{Int}(map(k -> sum(z .== k), 1:K))\n",
        "\n",
        "        # Draw the latent assignment.\n",
        "        z[i] ~ ChineseRestaurantProcess(rpm, nk)\n",
        "\n",
        "        # Create a new cluster?\n",
        "        if z[i] > K\n",
        "            push!(μ, 0.0)\n",
        "\n",
        "            # Draw location of new cluster.\n",
        "            μ[z[i]] ~ H\n",
        "        end\n",
        "\n",
        "        # Draw observation.\n",
        "        x[i] ~ Normal(μ[z[i]], 1.0)\n",
        "    end\n",
        "end\n",
        "```\n",
        "\n",
        "We can now use Turing to infer the assignments of some data points. First, we will create some random data that comes from three clusters, with means of 0, -5, and 10.\n",
        "\n",
        "```{julia}\n",
        "using Plots, Random\n",
        "\n",
        "# Generate some test data.\n",
        "Random.seed!(1)\n",
        "data = vcat(randn(10), randn(10) .- 5, randn(10) .+ 10)\n",
        "data .-= mean(data)\n",
        "data /= std(data);\n",
        "```\n",
        "\n",
        "Next, we'll sample from our posterior using SMC.\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "setprogress!(false)\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "# MCMC sampling\n",
        "Random.seed!(2)\n",
        "iterations = 1000\n",
        "model_fun = infiniteGMM(data);\n",
        "chain = sample(model_fun, SMC(), iterations);\n",
        "```\n",
        "\n",
        "Finally, we can plot the number of clusters in each sample.\n",
        "\n",
        "```{julia}\n",
        "# Extract the number of clusters for each sample of the Markov chain.\n",
        "k = map(\n",
        "    t -> length(unique(vec(chain[t, MCMCChains.namesingroup(chain, :z), :].value))),\n",
        "    1:iterations,\n",
        ");\n",
        "\n",
        "# Visualize the number of clusters.\n",
        "plot(k; xlabel=\"Iteration\", ylabel=\"Number of clusters\", label=\"Chain 1\")\n",
        "```\n",
        "\n",
        "If we visualize the histogram of the number of clusters sampled from our posterior, we observe that the model seems to prefer 3 clusters, which is the true number of clusters. Note that the number of clusters in a Dirichlet process mixture model is not limited a priori and will grow to infinity with probability one. However, if conditioned on data the posterior will concentrate on a finite number of clusters enforcing the resulting model to have a finite amount of clusters. It is, however, not given that the posterior of a Dirichlet process Gaussian mixture model converges to the true number of clusters, given that data comes from a finite mixture model. See Jeffrey Miller and Matthew Harrison: [A simple example of Dirichlet process mixture inconsitency for the number of components](https://arxiv.org/pdf/1301.2708.pdf) for details.\n",
        "\n",
        "```{julia}\n",
        "histogram(k; xlabel=\"Number of clusters\", legend=false)\n",
        "```\n",
        "\n",
        "One issue with the Chinese restaurant process construction is that the number of latent parameters we need to sample scales with the number of observations. It may be desirable to use alternative constructions in certain cases. Alternative methods of constructing a Dirichlet process can be employed via the following representations:\n",
        "\n",
        "Size-Biased Sampling Process\n",
        "\n",
        "$$\n",
        "j_k \\sim \\mathrm{Beta}(1, \\alpha) \\cdot \\mathrm{surplus}\n",
        "$$\n",
        "\n",
        "Stick-Breaking Process\n",
        "$$\n",
        "v_k \\sim \\mathrm{Beta}(1, \\alpha)\n",
        "$$\n",
        "\n",
        "Chinese Restaurant Process\n",
        "$$\n",
        "p(z_n = k | z_{1:n-1}) \\propto \\begin{cases}\n",
        "\\frac{m_k}{n-1+\\alpha}, \\text{ if } m_k > 0\\\\\\\n",
        "\\frac{\\alpha}{n-1+\\alpha}\n",
        "\\end{cases}\n",
        "$$\n",
        "\n",
        "For more details see [this article](https://www.stats.ox.ac.uk/%7Eteh/research/npbayes/Teh2010a.pdf)."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}