{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Bayesian Poisson Regression\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../07-poisson-regression/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "This notebook is ported from the [example notebook](https://www.pymc.io/projects/examples/en/latest/generalized_linear_models/GLM-poisson-regression.html) of PyMC3 on Poisson Regression.\n",
        "\n",
        "[Poisson Regression](https://en.wikipedia.org/wiki/Poisson_regression) is a technique commonly used to model count data.\n",
        "Some of the applications include predicting the number of people defaulting on their loans or the number of cars running on a highway on a given day.\n",
        "This example describes a method to implement the Bayesian version of this technique using Turing.\n",
        "\n",
        "We will generate the dataset that we will be working on which describes the relationship between number of times a person sneezes during the day with his alcohol consumption and medicinal intake.\n",
        "\n",
        "We start by importing the required libraries.\n",
        "\n",
        "```{julia}\n",
        "#Import Turing, Distributions and DataFrames\n",
        "using Turing, Distributions, DataFrames, Distributed\n",
        "\n",
        "# Import MCMCChain, Plots, and StatsPlots for visualizations and diagnostics.\n",
        "using MCMCChains, Plots, StatsPlots\n",
        "\n",
        "# Set a seed for reproducibility.\n",
        "using Random\n",
        "Random.seed!(12);\n",
        "```\n",
        "\n",
        "# Generating data\n",
        "\n",
        "We start off by creating a toy dataset. We take the case of a person who takes medicine to prevent excessive sneezing. Alcohol consumption increases the rate of sneezing for that person. Thus, the two factors affecting the number of sneezes in a given day are alcohol consumption and whether the person has taken his medicine. Both these variable are taken as boolean valued while the number of sneezes will be a count valued variable. We also take into consideration that the interaction between the two boolean variables will affect the number of sneezes\n",
        "\n",
        "5 random rows are printed from the generated data to get a gist of the data generated.\n",
        "\n",
        "```{julia}\n",
        "theta_noalcohol_meds = 1    # no alcohol, took medicine\n",
        "theta_alcohol_meds = 3      # alcohol, took medicine\n",
        "theta_noalcohol_nomeds = 6  # no alcohol, no medicine\n",
        "theta_alcohol_nomeds = 36   # alcohol, no medicine\n",
        "\n",
        "# no of samples for each of the above cases\n",
        "q = 100\n",
        "\n",
        "#Generate data from different Poisson distributions\n",
        "noalcohol_meds = Poisson(theta_noalcohol_meds)\n",
        "alcohol_meds = Poisson(theta_alcohol_meds)\n",
        "noalcohol_nomeds = Poisson(theta_noalcohol_nomeds)\n",
        "alcohol_nomeds = Poisson(theta_alcohol_nomeds)\n",
        "\n",
        "nsneeze_data = vcat(\n",
        "    rand(noalcohol_meds, q),\n",
        "    rand(alcohol_meds, q),\n",
        "    rand(noalcohol_nomeds, q),\n",
        "    rand(alcohol_nomeds, q),\n",
        ")\n",
        "alcohol_data = vcat(zeros(q), ones(q), zeros(q), ones(q))\n",
        "meds_data = vcat(zeros(q), zeros(q), ones(q), ones(q))\n",
        "\n",
        "df = DataFrame(;\n",
        "    nsneeze=nsneeze_data,\n",
        "    alcohol_taken=alcohol_data,\n",
        "    nomeds_taken=meds_data,\n",
        "    product_alcohol_meds=meds_data .* alcohol_data,\n",
        ")\n",
        "df[sample(1:nrow(df), 5; replace=false), :]\n",
        "```\n",
        "\n",
        "# Visualisation of the dataset\n",
        "\n",
        "We plot the distribution of the number of sneezes for the 4 different cases taken above. As expected, the person sneezes the most when he has taken alcohol and not taken his medicine. He sneezes the least when he doesn't consume alcohol and takes his medicine.\n",
        "\n",
        "```{julia}\n",
        "# Data Plotting\n",
        "\n",
        "p1 = Plots.histogram(\n",
        "    df[(df[:, :alcohol_taken] .== 0) .& (df[:, :nomeds_taken] .== 0), 1];\n",
        "    title=\"no_alcohol+meds\",\n",
        ")\n",
        "p2 = Plots.histogram(\n",
        "    (df[(df[:, :alcohol_taken] .== 1) .& (df[:, :nomeds_taken] .== 0), 1]);\n",
        "    title=\"alcohol+meds\",\n",
        ")\n",
        "p3 = Plots.histogram(\n",
        "    (df[(df[:, :alcohol_taken] .== 0) .& (df[:, :nomeds_taken] .== 1), 1]);\n",
        "    title=\"no_alcohol+no_meds\",\n",
        ")\n",
        "p4 = Plots.histogram(\n",
        "    (df[(df[:, :alcohol_taken] .== 1) .& (df[:, :nomeds_taken] .== 1), 1]);\n",
        "    title=\"alcohol+no_meds\",\n",
        ")\n",
        "plot(p1, p2, p3, p4; layout=(2, 2), legend=false)\n",
        "```\n",
        "\n",
        "We must convert our `DataFrame` data into the `Matrix` form as the manipulations that we are about are designed to work with `Matrix` data. We also separate the features from the labels which will be later used by the Turing sampler to generate samples from the posterior.\n",
        "\n",
        "```{julia}\n",
        "# Convert the DataFrame object to matrices.\n",
        "data = Matrix(df[:, [:alcohol_taken, :nomeds_taken, :product_alcohol_meds]])\n",
        "data_labels = df[:, :nsneeze]\n",
        "data\n",
        "```\n",
        "\n",
        "We must recenter our data about 0 to help the Turing sampler in initialising the parameter estimates. So, normalising the data in each column by subtracting the mean and dividing by the standard deviation:\n",
        "\n",
        "```{julia}\n",
        "# Rescale our matrices.\n",
        "data = (data .- mean(data; dims=1)) ./ std(data; dims=1)\n",
        "```\n",
        "\n",
        "# Declaring the Model: Poisson Regression\n",
        "\n",
        "Our model, `poisson_regression` takes four arguments:\n",
        "\n",
        "  - `x` is our set of independent variables;\n",
        "  - `y` is the element we want to predict;\n",
        "  - `n` is the number of observations we have; and\n",
        "  - `σ²` is the standard deviation we want to assume for our priors.\n",
        "\n",
        "Within the model, we create four coefficients (`b0`, `b1`, `b2`, and `b3`) and assign a prior of normally distributed with means of zero and standard deviations of `σ²`. We want to find values of these four coefficients to predict any given `y`.\n",
        "\n",
        "Intuitively, we can think of the coefficients as:\n",
        "\n",
        "  - `b1` is the coefficient which represents the effect of taking alcohol on the number of sneezes;\n",
        "  - `b2` is the coefficient which represents the effect of taking in no medicines on the number of sneezes;\n",
        "  - `b3` is the coefficient which represents the effect of interaction between taking alcohol and no medicine on the number of sneezes;\n",
        "\n",
        "The `for` block creates a variable `theta` which is the weighted combination of the input features. We have defined the priors on these weights above. We then observe the likelihood of calculating `theta` given the actual label, `y[i]`.\n",
        "\n",
        "```{julia}\n",
        "# Bayesian poisson regression (LR)\n",
        "@model function poisson_regression(x, y, n, σ²)\n",
        "    b0 ~ Normal(0, σ²)\n",
        "    b1 ~ Normal(0, σ²)\n",
        "    b2 ~ Normal(0, σ²)\n",
        "    b3 ~ Normal(0, σ²)\n",
        "    for i in 1:n\n",
        "        theta = b0 + b1 * x[i, 1] + b2 * x[i, 2] + b3 * x[i, 3]\n",
        "        y[i] ~ Poisson(exp(theta))\n",
        "    end\n",
        "end;\n",
        "```\n",
        "\n",
        "# Sampling from the posterior\n",
        "\n",
        "We use the `NUTS` sampler to sample values from the posterior. We run multiple chains using the `MCMCThreads()` function to nullify the effect of a problematic chain. We then use the Gelman, Rubin, and Brooks Diagnostic to check the convergence of these multiple chains.\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "# Retrieve the number of observations.\n",
        "n, _ = size(data)\n",
        "\n",
        "# Sample using NUTS.\n",
        "\n",
        "num_chains = 4\n",
        "m = poisson_regression(data, data_labels, n, 10)\n",
        "chain = sample(m, NUTS(), MCMCThreads(), 2_500, num_chains; discard_adapt=false, progress=false)\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "chain\n",
        "```\n",
        "\n",
        "::: {.callout-warning collapse=\"true\"}\n",
        "## Sampling With Multiple Threads\n",
        "The `sample()` call above assumes that you have at least `nchains` threads available in your Julia instance. If you do not, the multiple chains \n",
        "will run sequentially, and you may notice a warning. For more information, see [the Turing documentation on sampling multiple chains.]({{<meta core-functionality>}}#sampling-multiple-chains)\n",
        ":::\n",
        "\n",
        "# Viewing the Diagnostics\n",
        "\n",
        "We use the Gelman, Rubin, and Brooks Diagnostic to check whether our chains have converged. Note that we require multiple chains to use this diagnostic which analyses the difference between these multiple chains.\n",
        "\n",
        "We expect the chains to have converged. This is because we have taken sufficient number of iterations (1500) for the NUTS sampler. However, in case the test fails, then we will have to take a larger number of iterations, resulting in longer computation time.\n",
        "\n",
        "```{julia}\n",
        "gelmandiag(chain)\n",
        "```\n",
        "\n",
        "From the above diagnostic, we can conclude that the chains have converged because the PSRF values of the coefficients are close to 1.\n",
        "\n",
        "So, we have obtained the posterior distributions of the parameters. We transform the coefficients and recover theta values by taking the exponent of the meaned values of the coefficients `b0`, `b1`, `b2` and `b3`. We take the exponent of the means to get a better comparison of the relative values of the coefficients. We then compare this with the intuitive meaning that was described earlier.\n",
        "\n",
        "```{julia}\n",
        "# Taking the first chain\n",
        "c1 = chain[:, :, 1]\n",
        "\n",
        "# Calculating the exponentiated means\n",
        "b0_exp = exp(mean(c1[:b0]))\n",
        "b1_exp = exp(mean(c1[:b1]))\n",
        "b2_exp = exp(mean(c1[:b2]))\n",
        "b3_exp = exp(mean(c1[:b3]))\n",
        "\n",
        "print(\"The exponent of the meaned values of the weights (or coefficients are): \\n\")\n",
        "println(\"b0: \", b0_exp)\n",
        "println(\"b1: \", b1_exp)\n",
        "println(\"b2: \", b2_exp)\n",
        "println(\"b3: \", b3_exp)\n",
        "print(\"The posterior distributions obtained after sampling can be visualised as :\\n\")\n",
        "```\n",
        "\n",
        "Visualising the posterior by plotting it:\n",
        "\n",
        "```{julia}\n",
        "plot(chain)\n",
        "```\n",
        "\n",
        "# Interpreting the Obtained Mean Values\n",
        "\n",
        "The exponentiated mean of the coefficient `b1` is roughly half of that of `b2`. This makes sense because in the data that we generated, the number of sneezes was more sensitive to the medicinal intake as compared to the alcohol consumption. We also get a weaker dependence on the interaction between the alcohol consumption and the medicinal intake as can be seen from the value of `b3`.\n",
        "\n",
        "# Removing the Warmup Samples\n",
        "\n",
        "As can be seen from the plots above, the parameters converge to their final distributions after a few iterations.\n",
        "The initial values during the warmup phase increase the standard deviations of the parameters and are not required after we get the desired distributions.\n",
        "Thus, we remove these warmup values and once again view the diagnostics.\n",
        "To remove these warmup values, we take all values except the first 200.\n",
        "This is because we set the second parameter of the NUTS sampler (which is the number of adaptations) to be equal to 200.\n",
        "\n",
        "```{julia}\n",
        "chains_new = chain[201:end, :, :]\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "plot(chains_new)\n",
        "```\n",
        "\n",
        "As can be seen from the numeric values and the plots above, the standard deviation values have decreased and all the plotted values are from the estimated posteriors. The exponentiated mean values, with the warmup samples removed, have not changed by much and they are still in accordance with their intuitive meanings as described earlier."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}