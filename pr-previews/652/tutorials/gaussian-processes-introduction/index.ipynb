{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: \"Gaussian Processes: Introduction\"\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../15-gaussian-processes/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "[JuliaGPs](https://github.com/JuliaGaussianProcesses/#welcome-to-juliagps) packages integrate well with Turing.jl because they implement the Distributions.jl\n",
        "interface.\n",
        "You should be able to understand what is going on in this tutorial if you know what a GP is.\n",
        "For a more in-depth understanding of the\n",
        "[JuliaGPs](https://github.com/JuliaGaussianProcesses/#welcome-to-juliagps) functionality\n",
        "used here, please consult the\n",
        "[JuliaGPs](https://github.com/JuliaGaussianProcesses/#welcome-to-juliagps) docs.\n",
        "\n",
        "In this tutorial, we will model the putting dataset discussed in Chapter 21 of\n",
        "[Bayesian Data Analysis](http://www.stat.columbia.edu/%7Egelman/book/).\n",
        "The dataset comprises the result of measuring how often a golfer successfully gets the ball\n",
        "in the hole, depending on how far away from it they are.\n",
        "The goal of inference is to estimate the probability of any given shot being successful at a\n",
        "given distance.\n",
        "\n",
        "### Let's download the data and take a look at it:\n",
        "\n",
        "```{julia}\n",
        "using CSV, DataFrames\n",
        "\n",
        "df = CSV.read(\"golf.dat\", DataFrame; delim=' ', ignorerepeated=true)\n",
        "df[1:5, :]\n",
        "```\n",
        "\n",
        "We've printed the first 5 rows of the dataset (which comprises only 19 rows in total).\n",
        "Observe it has three columns:\n",
        "\n",
        " 1. `distance` -- how far away from the hole. I'll refer to `distance` as `d` throughout the rest of this tutorial\n",
        " 2. `n` -- how many shots were taken from a given distance\n",
        " 3. `y` -- how many shots were successful from a given distance\n",
        "\n",
        "We will use a Binomial model for the data, whose success probability is parametrised by a\n",
        "transformation of a GP. Something along the lines of:\n",
        "$$\n",
        "\\begin{aligned}\n",
        "f & \\sim \\operatorname{GP}(0, k) \\\\\n",
        "y_j \\mid f(d_j) & \\sim \\operatorname{Binomial}(n_j, g(f(d_j))) \\\\\n",
        "g(x) & := \\frac{1}{1 + e^{-x}}\n",
        "\\end{aligned}\n",
        "$$\n",
        "\n",
        "To do this, let's define our Turing.jl model:\n",
        "\n",
        "```{julia}\n",
        "using AbstractGPs, LogExpFunctions, Turing\n",
        "\n",
        "@model function putting_model(d, n; jitter=1e-4)\n",
        "    v ~ Gamma(2, 1)\n",
        "    l ~ Gamma(4, 1)\n",
        "    f = GP(v * with_lengthscale(SEKernel(), l))\n",
        "    f_latent ~ f(d, jitter)\n",
        "    y ~ product_distribution(Binomial.(n, logistic.(f_latent)))\n",
        "    return (fx=f(d, jitter), f_latent=f_latent, y=y)\n",
        "end\n",
        "```\n",
        "\n",
        "We first define an `AbstractGPs.GP`, which represents a distribution over functions, and\n",
        "is entirely separate from Turing.jl.\n",
        "We place a prior over its variance `v` and length-scale `l`.\n",
        "`f(d, jitter)` constructs the multivariate Gaussian comprising the random variables\n",
        "in `f` whose indices are in `d` (plus a bit of independent Gaussian noise with variance\n",
        "`jitter` -- see [the docs](https://juliagaussianprocesses.github.io/AbstractGPs.jl/dev/api/#FiniteGP-and-AbstractGP)\n",
        "for more details).\n",
        "`f(d, jitter)` has the type `AbstractMvNormal`, and is the bit of AbstractGPs.jl that implements the\n",
        "Distributions.jl interface, so it's legal to put it on the right-hand side\n",
        "of a `~`.\n",
        "From this you should deduce that `f_latent` is distributed according to a multivariate\n",
        "Gaussian.\n",
        "The remaining lines comprise standard Turing.jl code that is encountered in other tutorials\n",
        "and Turing documentation.\n",
        "\n",
        "Before performing inference, we might want to inspect the prior that our model places over\n",
        "the data, to see whether there is anything obviously wrong.\n",
        "These kinds of prior predictive checks are straightforward to perform using Turing.jl, since\n",
        "it is possible to sample from the prior easily by just calling the model:\n",
        "\n",
        "```{julia}\n",
        "m = putting_model(Float64.(df.distance), df.n)\n",
        "m().y\n",
        "```\n",
        "\n",
        "We make use of this to see what kinds of datasets we simulate from the prior:\n",
        "\n",
        "```{julia}\n",
        "using Plots\n",
        "\n",
        "function plot_data(d, n, y, xticks, yticks)\n",
        "    ylims = (0, round(maximum(n), RoundUp; sigdigits=2))\n",
        "    margin = -0.5 * Plots.mm\n",
        "    plt = plot(; xticks=xticks, yticks=yticks, ylims=ylims, margin=margin, grid=false)\n",
        "    bar!(plt, d, n; color=:red, label=\"\", alpha=0.5)\n",
        "    bar!(plt, d, y; label=\"\", color=:blue, alpha=0.7)\n",
        "    return plt\n",
        "end\n",
        "\n",
        "# Construct model and run some prior predictive checks.\n",
        "m = putting_model(Float64.(df.distance), df.n)\n",
        "hists = map(1:20) do j\n",
        "    xticks = j > 15 ? :auto : nothing\n",
        "    yticks = rem(j, 5) == 1 ? :auto : nothing\n",
        "    return plot_data(df.distance, df.n, m().y, xticks, yticks)\n",
        "end\n",
        "plot(hists...; layout=(4, 5))\n",
        "```\n",
        "\n",
        "In this case, the only prior knowledge I have is that the proportion of successful shots\n",
        "ought to decrease monotonically as the distance from the hole increases, which should show\n",
        "up in the data as the blue lines generally go down as we move from left to right on each\n",
        "graph.\n",
        "Unfortunately, there is not a simple way to enforce monotonicity in the samples from a GP,\n",
        "and we can see this in some of the plots above, so we must hope that we have enough data to\n",
        "ensure that this relationship holds approximately under the posterior.\n",
        "In any case, you can judge for yourself whether you think this is the most useful\n",
        "visualisation that we can perform -- if you think there is something better to look at,\n",
        "please let us know!\n",
        "\n",
        "Moving on, we generate samples from the posterior using the default `NUTS` sampler.\n",
        "We'll make use of [ReverseDiff.jl](https://github.com/JuliaDiff/ReverseDiff.jl), as it has\n",
        "better performance than [ForwardDiff.jl](https://github.com/JuliaDiff/ForwardDiff.jl/) on\n",
        "this example. See the [automatic differentiation docs]({{< meta usage-automatic-differentiation >}}) for more info.\n",
        "\n",
        "\n",
        "```{julia}\n",
        "using Random, ReverseDiff\n",
        "\n",
        "m_post = m | (y=df.y,)\n",
        "chn = sample(Xoshiro(123456), m_post, NUTS(; adtype=AutoReverseDiff()), 1_000, progress=false)\n",
        "```\n",
        "\n",
        "We can use these samples and the `posterior` function from `AbstractGPs` to sample from the\n",
        "posterior probability of success at any distance we choose:\n",
        "\n",
        "```{julia}\n",
        "d_pred = 1:0.2:21\n",
        "samples = map(returned(m_post, chn)[1:10:end]) do x\n",
        "    return logistic.(rand(posterior(x.fx, x.f_latent)(d_pred, 1e-4)))\n",
        "end\n",
        "p = plot()\n",
        "plot!(d_pred, reduce(hcat, samples); label=\"\", color=:blue, alpha=0.2)\n",
        "scatter!(df.distance, df.y ./ df.n; label=\"\", color=:red)\n",
        "```\n",
        "\n",
        "We can see that the general trend is indeed down as the distance from the hole increases,\n",
        "and that if we move away from the data, the posterior uncertainty quickly inflates.\n",
        "This suggests that the model is probably going to do a reasonable job of interpolating\n",
        "between observed data, but less good a job at extrapolating to larger distances."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}