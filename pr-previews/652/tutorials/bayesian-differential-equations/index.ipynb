{
  "cells": [
    {
      "outputs": [],
      "cell_type": "code",
      "source": "# Install necessary dependencies.\nusing Pkg\nPkg.activate(; temp=true)\nPkg.add([\"Turing\", \"DifferentialEquations\", \"Distributions\", \"LinearAlgebra\", \"StatsPlots\", \"Mooncake\", \"SciMLSensitivity\", \"Random\"])",
      "metadata": {},
      "execution_count": null
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "#| echo: false\n#| output: false\nusing Pkg;\nPkg.instantiate();",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "A basic scientific problem is to mathematically model a system of interest, then compare this model to the observable reality around us.\nSuch models often involve dynamical systems of differential equations.\nIn practice, these equations often have unknown parameters we would like to estimate.\nThe \"[forward problem](https://en.wikipedia.org/wiki/Well-posed_problem)\" of simulation consists of solving the differential equations for a given set of parameters, while the \"[inverse problem](https://en.wikipedia.org/wiki/Inverse_problem)\" of parameter estimation uses observed data to infer the unknown model parameters.\nBayesian inference provides a robust approach to parameter estimation with quantified uncertainty.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "using Turing\nusing DifferentialEquations\n# Load StatsPlots for visualizations and diagnostics.\nusing StatsPlots\nusing LinearAlgebra\nusing Distributions\n# Set a seed for reproducibility.\nusing Random\nRandom.seed!(14);",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "## The Lotka–Volterra Model\n\n[The Lotka–Volterra equations](https://en.wikipedia.org/wiki/Lotka%E2%80%93Volterra_equations), also known as the predator–prey equations, are a pair of first-order nonlinear differential equations.\nThese differential equations are frequently used to describe the dynamics of biological systems in which two species interact, one as a predator and the other as prey.\nThe populations change through time according to the pair of equations\n\n$$\n\\begin{aligned}\n\\frac{\\mathrm{d}x}{\\mathrm{d}t} &= (\\alpha - \\beta y(t))x(t), \\\\\n\\frac{\\mathrm{d}y}{\\mathrm{d}t} &= (\\delta x(t) - \\gamma)y(t),\n\\end{aligned}\n$$\n\nwhere $x(t)$ and $y(t)$ denote the populations of prey and predator at time $t$, respectively, and $\\alpha, \\beta, \\gamma, \\delta$ are positive parameters.\nIn the absence of predators, the prey population $x$ would increase exponentially at rate $\\alpha$ (with dimensions of time^-1^).\nHowever, the predators kill some prey at a rate $\\beta$ (prey predator^-1^ time^-1^), which enables the predator population to increase at rate $\\delta$ (predators prey^-1^ time^-1^).\nFinally, predators are removed by natural mortality at rate $\\gamma$ (time^-1^).\n\nWe implement the Lotka–Volterra model and simulate it with parameters $\\alpha = 1.5$, $\\beta = 1$, $\\gamma = 3$, and $\\delta = 1$ and initial conditions $x(0) = y(0) = 1$.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "# Define Lotka–Volterra model.\nfunction lotka_volterra(du, u, p, t)\n    # Model parameters.\n    α, β, γ, δ = p\n    # Current state.\n    x, y = u\n\n    # Evaluate differential equations.\n    du[1] = (α - β * y) * x # prey\n    du[2] = (δ * x - γ) * y # predator\n\n    return nothing\nend\n\n# Define initial-value problem.\nu0 = [1.0, 1.0]\np = [1.5, 1.0, 3.0, 1.0]\ntspan = (0.0, 10.0)\nprob = ODEProblem(lotka_volterra, u0, tspan, p)\n\n# Plot simulation.\nplot(solve(prob, Tsit5()))",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "We generate noisy observations to use for the parameter estimation tasks in this tutorial.\nWith the [`saveat` argument](https://docs.sciml.ai/DiffEqDocs/stable/basics/common_solver_opts/) to the differential equation solver, we specify that the solution is stored only at `0.1` time units.\n\nTo make the example more realistic, we generate data as random [Poisson](https://en.wikipedia.org/wiki/Poisson_distribution) counts based on the \"true\" population densities of predator and prey from the simulation.\nPoisson-distributed data are common in ecology (for instance, counts of animals detected by a camera trap).\nWe assume the Poisson rate parameter $\\lambda$ is proportional to the underlying animal densities, with proportionality constant $q = 1.7$ (representing observation efficiency).",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "sol = solve(prob, Tsit5(); saveat=0.1)\nq = 1.7\nodedata = rand.(Poisson.(q * Array(sol)))\n\n# Plot simulation and noisy observations.\nplot(sol, label=[\"Prey\" \"Predator\"])\nscatter!(sol.t, odedata'; color=[1 2], label=\"\")",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "An even more realistic example could be fitted to the famous hare-and-lynx system using the long-term trapping records of the Hudson's Bay Company.\nA Stan implementation of this problem with slightly different priors can be found [here](https://mc-stan.org/learn-stan/case-studies/lotka-volterra-predator-prey.html).\nFor this tutorial, though, we will stick with simulated data.\n\n## Direct Handling of Bayesian Estimation with Turing\n\n[DifferentialEquations.jl](https://docs.sciml.ai/DiffEqDocs/stable/) is the main Julia package for numerically solving differential equations.\nIts functionality is completely interoperable with Turing.jl, which means that we can directly simulate differential equations inside a Turing `@model`.\n\nFor the purposes of this tutorial, we choose priors for the parameters that are quite close to the ground truth.\nAs justification, we can imagine we have preexisting estimates for the biological rates.\nPractically, this helps us to illustrate the results without needing to run overly long MCMC chains.\n\nNote we also have to take special care with the ODE solver.\nFor certain parameter combinations, the numerical solver may predict animal densities that are just barely below zero.\nThis causes errors with the Poisson distribution, which needs a non-negative mean $\\lambda$.\nTo avoid this happening, we tell the solver to aim for small absolute and relative errors (`abstol=1e-6, reltol=1e-6`).\nWe also add a fudge factor `ϵ = 1e-5` to the predicted data.\nSince `ϵ` is greater than the solver's tolerance, it should overcome any remaining numerical error, making sure all predicted values are positive.\nAt the same time, it is so small compared to the data that it should have a negligible effect on inference.\nIf this approach doesn't work, there are some more ideas to try [here](https://docs.sciml.ai/DiffEqDocs/stable/basics/faq/#My-ODE-goes-negative-but-should-stay-positive,-what-tools-can-help?).\nIn the case of continuous observations (e.g. data derived from modelling chemical reactions), it is sufficient to use a normal distribution with the mean as the data point and an appropriately chosen variance (which can itself also be a parameter with a prior distribution).",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "@model function fitlv(data, prob)\n    # Prior distributions.\n    α ~ truncated(Normal(1.5, 0.2); lower=0.5, upper=2.5)\n    β ~ truncated(Normal(1.1, 0.2); lower=0, upper=2)\n    γ ~ truncated(Normal(3.0, 0.2); lower=1, upper=4)\n    δ ~ truncated(Normal(1.0, 0.2); lower=0, upper=2)\n    q ~ truncated(Normal(1.7, 0.2); lower=0, upper=3)\n\n    # Simulate Lotka–Volterra model. \n    p = [α, β, γ, δ]\n    predicted = solve(prob, Tsit5(); p=p, saveat=0.1, abstol=1e-6, reltol=1e-6)\n    ϵ = 1e-5\n    \n    # Observations.\n    for i in eachindex(predicted)\n        data[:, i] ~ arraydist(Poisson.(q .* predicted[i] .+ ϵ))\n    end\n\n    return nothing\nend\n\nmodel = fitlv(odedata, prob)\n\n# Sample 3 independent chains with forward-mode automatic differentiation (the default).\nchain = sample(model, NUTS(), MCMCSerial(), 1000, 3; progress=false)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "The estimated parameters are close to the parameter values the observations were generated with. We can also check visually that the chains have converged.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "plot(chain)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "### Data retrodiction\n\nIn Bayesian analysis it is often useful to retrodict the data, i.e. generate simulated data using samples from the posterior distribution, and compare to the original data (see for instance section 3.3.2 - model checking of McElreath's book \"Statistical Rethinking\").\nHere, we solve the ODE for 300 randomly picked posterior samples in the `chain`.\nWe plot the ensemble of solutions to check if the solution resembles the data.\nThe 300 retrodicted time courses from the posterior are plotted in gray, the noisy observations are shown as blue and red dots, and the green and purple lines are the ODE solution that was used to generate the data.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "plot(; legend=false)\nposterior_samples = sample(chain[[:α, :β, :γ, :δ]], 300; replace=false)\nfor p in eachrow(Array(posterior_samples))\n    sol_p = solve(prob, Tsit5(); p=p, saveat=0.1)\n    plot!(sol_p; alpha=0.1, color=\"#BBBBBB\")\nend\n\n# Plot simulation and noisy observations.\nplot!(sol; color=[1 2], linewidth=1)\nscatter!(sol.t, odedata'; color=[1 2])",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "We can see that, even though we added quite a bit of noise to the data the posterior distribution reproduces quite accurately the \"true\" ODE solution.\n\n## Lotka–Volterra model without data of prey\n\nOne can also perform parameter inference for a Lotka–Volterra model with incomplete data.\nFor instance, let us suppose we have only observations of the predators but not of the prey.\nWe can fit the model only to the $y$ variable of the system without providing any data for $x$:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "@model function fitlv2(data::AbstractVector, prob)\n    # Prior distributions.\n    α ~ truncated(Normal(1.5, 0.2); lower=0.5, upper=2.5)\n    β ~ truncated(Normal(1.1, 0.2); lower=0, upper=2)\n    γ ~ truncated(Normal(3.0, 0.2); lower=1, upper=4)\n    δ ~ truncated(Normal(1.0, 0.2); lower=0, upper=2)\n    q ~ truncated(Normal(1.7, 0.2); lower=0, upper=3)\n\n    # Simulate Lotka–Volterra model but save only the second state of the system (predators).\n    p = [α, β, γ, δ]\n    predicted = solve(prob, Tsit5(); p=p, saveat=0.1, save_idxs=2, abstol=1e-6, reltol=1e-6)\n    ϵ = 1e-5\n\n    # Observations of the predators.\n    data ~ arraydist(Poisson.(q .* predicted.u .+ ϵ))\n\n    return nothing\nend\n\nmodel2 = fitlv2(odedata[2, :], prob)\n\n# Sample 3 independent chains.\nchain2 = sample(model2, NUTS(0.45), MCMCSerial(), 5000, 3; progress=false)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "Again we inspect the trajectories of 300 randomly selected posterior samples.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "plot(; legend=false)\nposterior_samples = sample(chain2[[:α, :β, :γ, :δ]], 300; replace=false)\nfor p in eachrow(Array(posterior_samples))\n    sol_p = solve(prob, Tsit5(); p=p, saveat=0.1)\n    plot!(sol_p; alpha=0.1, color=\"#BBBBBB\")\nend\n\n# Plot simulation and noisy observations.\nplot!(sol; color=[1 2], linewidth=1)\nscatter!(sol.t, odedata'; color=[1 2])",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "Note that here the observations of the prey (blue dots) were not used in the parameter estimation!\nYet, the model can predict the values of $x$ relatively accurately, albeit with a wider distribution of solutions, reflecting the greater uncertainty in the prediction of the $x$ values.\n\n## Inference of Delay Differential Equations\n\nHere we show an example of inference with another type of differential equation: a delay differential equation (DDE).\nDDEs are differential equations where derivatives are functions of values at an earlier point in time.\nThis is useful to model a delayed effect, such as the incubation time of a virus.\n\nHere is a delayed version of the Lotka–Volterra system:\n\n$$\n\\begin{aligned}\n\\frac{\\mathrm{d}x}{\\mathrm{d}t} &= \\alpha x(t-\\tau) - \\beta y(t) x(t),\\\\\n\\frac{\\mathrm{d}y}{\\mathrm{d}t} &= - \\gamma y(t) + \\delta x(t) y(t),\n\\end{aligned}\n$$\n\nwhere $\\tau$ is a (positive) delay and $x(t-\\tau)$ is the variable $x$ at an earlier time point $t - \\tau$.\n\nThe initial-value problem of the delayed system can be implemented as a `DDEProblem`.\nAs described in the [DDE example](https://diffeq.sciml.ai/stable/tutorials/dde_example/), here the function `h` is the history function that can be used to obtain a state at an earlier time point.\nAgain we use parameters $\\alpha = 1.5$, $\\beta = 1$, $\\gamma = 3$, and $\\delta = 1$ and initial conditions $x(0) = y(0) = 1$.\nMoreover, we assume $x(t) = 1$ for $t < 0$.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "function delay_lotka_volterra(du, u, h, p, t)\n    # Model parameters.\n    α, β, γ, δ = p\n\n    # Current state.\n    x, y = u\n    # Evaluate differential equations\n    du[1] = α * h(p, t - 1; idxs=1) - β * x * y\n    du[2] = -γ * y + δ * x * y\n\n    return nothing\nend\n\n# Define initial-value problem.\np = (1.5, 1.0, 3.0, 1.0)\nu0 = [1.0; 1.0]\ntspan = (0.0, 10.0)\nh(p, t; idxs::Int) = 1.0\nprob_dde = DDEProblem(delay_lotka_volterra, u0, h, tspan, p);",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "We generate observations by sampling from the corresponding Poisson distributions derived from the simulation results:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "sol_dde = solve(prob_dde; saveat=0.1)\nddedata = rand.(Poisson.(q .* Array(sol_dde)))\n\n# Plot simulation and noisy observations.\nplot(sol_dde)\nscatter!(sol_dde.t, ddedata'; color=[1 2], label=\"\")",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "Now we define the Turing model for the Lotka–Volterra model with a delay, and sample 3 independent chains.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "@model function fitlv_dde(data, prob)\n    # Prior distributions.\n    α ~ truncated(Normal(1.5, 0.2); lower=0.5, upper=2.5)\n    β ~ truncated(Normal(1.1, 0.2); lower=0, upper=2)\n    γ ~ truncated(Normal(3.0, 0.2); lower=1, upper=4)\n    δ ~ truncated(Normal(1.0, 0.2); lower=0, upper=2)\n    q ~ truncated(Normal(1.7, 0.2); lower=0, upper=3)\n\n    # Simulate Lotka–Volterra model.\n    p = [α, β, γ, δ]\n    predicted = solve(prob, MethodOfSteps(Tsit5()); p=p, saveat=0.1, abstol=1e-6, reltol=1e-6)\n    ϵ = 1e-5\n\n    # Observations.\n    for i in eachindex(predicted)\n        data[:, i] ~ arraydist(Poisson.(q .* predicted[i] .+ ϵ))\n    end\nend\n\nmodel_dde = fitlv_dde(ddedata, prob_dde)\n\nchain_dde = sample(model_dde, NUTS(), MCMCSerial(), 300, 3; progress=false)",
      "metadata": {},
      "execution_count": null
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "plot(chain_dde)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "Finally, we plot trajectories of 300 randomly selected samples from the posterior.\nAgain, the dots indicate our observations, the coloured lines are the \"true\" simulations without noise, and the gray lines are trajectories from the posterior samples.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "plot(; legend=false)\nposterior_samples = sample(chain_dde[[:α, :β, :γ, :δ]], 300; replace=false)\nfor p in eachrow(Array(posterior_samples))\n    sol_p = solve(prob_dde, MethodOfSteps(Tsit5()); p=p, saveat=0.1)\n    plot!(sol_p; alpha=0.1, color=\"#BBBBBB\")\nend\n\n# Plot simulation and noisy observations.\nplot!(sol_dde; color=[1 2], linewidth=1)\nscatter!(sol_dde.t, ddedata'; color=[1 2])",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "The fit is pretty good even though the data was quite noisy to start.\n\n## Scaling to Large Models: Adjoint Sensitivities\n\nTuring's gradient-based MCMC algorithms, such as NUTS, use ForwardDiff by default.\nThis works well for small models, but for larger models with many parameters, reverse-mode automatic differentiation is often more efficient (see [the automatic differentiation page]({{< meta usage-automatic-differentiation >}}) for more information).\n\nTo use reverse-mode AD with differential equations, you need to first load the [SciMLSensitivity.jl package](https://docs.sciml.ai/SciMLSensitivity/stable/), which forms part of SciML's differential equation suite.\nHere, 'sensitivity' refers to the derivative of the solution of a differential equation with respect to its parameters.\nMore details on the mathematical theory that underpins these methods can be found in [the SciMLSensitivity documentation](https://docs.sciml.ai/SciMLSensitivity/stable/sensitivity_math/).\n\nOnce SciMLSensitivity has been loaded, you can use one of the AD backends which are compatible with SciMLSensitivity.jl.\nFor example, if we wanted to use [Mooncake.jl](https://chalk-lab.github.io/Mooncake.jl/stable/), we could run:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "import Mooncake\nimport SciMLSensitivity\n\n# Define the AD backend to use\nadtype = AutoMooncake()\n\n# Sample a single chain with 1000 samples using Mooncake\nsample(model, NUTS(; adtype=adtype), 1000; progress=false)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "(If SciMLSensitivity is not loaded, the call to `sample` will error.)\n\nSciMLSensitivity has a number of sensitivity analysis algorithms: in this case it will automatically choose a default for you.\nYou can also manually specify an algorithm by providing the `sensealg` keyword argument to the `solve` function; the existing algorithms are covered in [this page of the SciMLSensitivity docs](https://docs.sciml.ai/SciMLSensitivity/stable/manual/differential_equation_sensitivities/).\n\nFor more examples of adjoint usage on large parameter models, consult the [DiffEqFlux documentation](https://docs.sciml.ai/DiffEqFlux/stable/).",
      "metadata": {}
    }
  ],
  "nbformat_minor": 5,
  "metadata": {
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia"
    },
    "kernelspec": {
      "name": "julia",
      "display_name": "Julia",
      "language": "julia"
    }
  },
  "nbformat": 4
}