{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Multinomial Logistic Regression\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../08-multinomial-logistic-regression/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "[Multinomial logistic regression](https://en.wikipedia.org/wiki/Multinomial_logistic_regression) is an extension of logistic regression. Logistic regression is used to model problems in which there are exactly two possible discrete outcomes. Multinomial logistic regression is used to model problems in which there are two or more possible discrete outcomes.\n",
        "\n",
        "In our example, we'll be using the iris dataset. The iris multiclass problem aims to predict the species of a flower given measurements (in centimeters) of sepal length and width and petal length and width. There are three possible species: Iris setosa, Iris versicolor, and Iris virginica.\n",
        "\n",
        "To start, let's import all the libraries we'll need.\n",
        "\n",
        "```{julia}\n",
        "# Load Turing.\n",
        "using Turing\n",
        "\n",
        "# Load RDatasets.\n",
        "using RDatasets\n",
        "\n",
        "# Load StatsPlots for visualizations and diagnostics.\n",
        "using StatsPlots\n",
        "\n",
        "# Functionality for splitting and normalizing the data.\n",
        "using MLDataUtils: shuffleobs, splitobs, rescale!\n",
        "\n",
        "# We need a softmax function which is provided by NNlib.\n",
        "using NNlib: softmax\n",
        "\n",
        "# Functionality for constructing arrays with identical elements efficiently.\n",
        "using FillArrays\n",
        "\n",
        "# Functionality for working with scaled identity matrices.\n",
        "using LinearAlgebra\n",
        "\n",
        "# Set a seed for reproducibility.\n",
        "using Random\n",
        "Random.seed!(0);\n",
        "```\n",
        "\n",
        "## Data Cleaning & Set Up\n",
        "\n",
        "Now we're going to import our dataset. Twenty rows of the dataset are shown below so you can get a good feel for what kind of data we have.\n",
        "\n",
        "```{julia}\n",
        "# Import the \"iris\" dataset.\n",
        "data = RDatasets.dataset(\"datasets\", \"iris\");\n",
        "\n",
        "# Show twenty random rows.\n",
        "data[rand(1:size(data, 1), 20), :]\n",
        "```\n",
        "\n",
        "In this data set, the outcome `Species` is currently coded as a string. We convert it to a numerical value by using indices `1`, `2`, and `3` to indicate species `setosa`, `versicolor`, and `virginica`, respectively.\n",
        "\n",
        "```{julia}\n",
        "# Recode the `Species` column.\n",
        "species = [\"setosa\", \"versicolor\", \"virginica\"]\n",
        "data[!, :Species_index] = indexin(data[!, :Species], species)\n",
        "\n",
        "# Show twenty random rows of the new species columns\n",
        "data[rand(1:size(data, 1), 20), [:Species, :Species_index]]\n",
        "```\n",
        "\n",
        "After we've done that tidying, it's time to split our dataset into training and testing sets, and separate the features and target from the data. Additionally, we must rescale our feature variables so that they are centered around zero by subtracting each column by the mean and dividing it by the standard deviation. Without this step, Turing's sampler will have a hard time finding a place to start searching for parameter estimates.\n",
        "\n",
        "```{julia}\n",
        "# Split our dataset 50%/50% into training/test sets.\n",
        "trainset, testset = splitobs(shuffleobs(data), 0.5)\n",
        "\n",
        "# Define features and target.\n",
        "features = [:SepalLength, :SepalWidth, :PetalLength, :PetalWidth]\n",
        "target = :Species_index\n",
        "\n",
        "# Turing requires data in matrix and vector form.\n",
        "train_features = Matrix(trainset[!, features])\n",
        "test_features = Matrix(testset[!, features])\n",
        "train_target = trainset[!, target]\n",
        "test_target = testset[!, target]\n",
        "\n",
        "# Standardize the features.\n",
        "μ, σ = rescale!(train_features; obsdim=1)\n",
        "rescale!(test_features, μ, σ; obsdim=1);\n",
        "```\n",
        "\n",
        "## Model Declaration\n",
        "\n",
        "Finally, we can define our model `logistic_regression`. It is a function that takes three arguments where\n",
        "\n",
        "  - `x` is our set of independent variables;\n",
        "  - `y` is the element we want to predict;\n",
        "  - `σ` is the standard deviation we want to assume for our priors.\n",
        "\n",
        "We select the `setosa` species as the baseline class (the choice does not matter). Then we create the intercepts and vectors of coefficients for the other classes against that baseline. More concretely, we create scalar intercepts `intercept_versicolor` and `intersept_virginica` and coefficient vectors `coefficients_versicolor` and `coefficients_virginica` with four coefficients each for the features `SepalLength`, `SepalWidth`, `PetalLength` and `PetalWidth`. We assume a normal distribution with mean zero and standard deviation `σ` as prior for each scalar parameter. We want to find the posterior distribution of these, in total ten, parameters to be able to predict the species for any given set of features.\n",
        "\n",
        "```{julia}\n",
        "# Bayesian multinomial logistic regression\n",
        "@model function logistic_regression(x, y, σ)\n",
        "    n = size(x, 1)\n",
        "    length(y) == n ||\n",
        "        throw(DimensionMismatch(\"number of observations in `x` and `y` is not equal\"))\n",
        "\n",
        "    # Priors of intercepts and coefficients.\n",
        "    intercept_versicolor ~ Normal(0, σ)\n",
        "    intercept_virginica ~ Normal(0, σ)\n",
        "    coefficients_versicolor ~ MvNormal(Zeros(4), σ^2 * I)\n",
        "    coefficients_virginica ~ MvNormal(Zeros(4), σ^2 * I)\n",
        "\n",
        "    # Compute the likelihood of the observations.\n",
        "    values_versicolor = intercept_versicolor .+ x * coefficients_versicolor\n",
        "    values_virginica = intercept_virginica .+ x * coefficients_virginica\n",
        "    for i in 1:n\n",
        "        # the 0 corresponds to the base category `setosa`\n",
        "        v = softmax([0, values_versicolor[i], values_virginica[i]])\n",
        "        y[i] ~ Categorical(v)\n",
        "    end\n",
        "end;\n",
        "```\n",
        "\n",
        "## Sampling\n",
        "\n",
        "Now we can run our sampler. This time we'll use [`NUTS`](https://turinglang.org/stable/docs/library/#Turing.Inference.NUTS) to sample from our posterior.\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "setprogress!(false)\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "m = logistic_regression(train_features, train_target, 1)\n",
        "chain = sample(m, NUTS(), MCMCThreads(), 1_500, 3)\n",
        "```\n",
        "\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "chain\n",
        "```\n",
        "\n",
        "::: {.callout-warning collapse=\"true\"}\n",
        "## Sampling With Multiple Threads\n",
        "The `sample()` call above assumes that you have at least `nchains` threads available in your Julia instance. If you do not, the multiple chains\n",
        "will run sequentially, and you may notice a warning. For more information, see [the Turing documentation on sampling multiple chains.]({{<meta core-functionality>}}#sampling-multiple-chains)\n",
        ":::\n",
        "\n",
        "Since we ran multiple chains, we may as well do a spot check to make sure each chain converges around similar points.\n",
        "\n",
        "```{julia}\n",
        "plot(chain)\n",
        "```\n",
        "\n",
        "Looks good!\n",
        "\n",
        "We can also use the `corner` function from MCMCChains to show the distributions of the various parameters of our multinomial logistic regression. The corner function requires MCMCChains and StatsPlots.\n",
        "\n",
        "```{julia}\n",
        "# Only plotting the first 3 coefficients due to a bug in Plots.jl\n",
        "corner(\n",
        "    chain,\n",
        "    MCMCChains.namesingroup(chain, :coefficients_versicolor)[1:3];\n",
        ")\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "# Only plotting the first 3 coefficients due to a bug in Plots.jl\n",
        "corner(\n",
        "    chain,\n",
        "    MCMCChains.namesingroup(chain, :coefficients_virginica)[1:3];\n",
        ")\n",
        "```\n",
        "\n",
        "Fortunately the corner plots appear to demonstrate unimodal distributions for each of our parameters, so it should be straightforward to take the means of each parameter's sampled values to estimate our model to make predictions.\n",
        "\n",
        "## Making Predictions\n",
        "\n",
        "How do we test how well the model actually predicts which of the three classes an iris flower belongs to? We need to build a `prediction` function that takes the test dataset and runs it through the average parameter calculated during sampling.\n",
        "\n",
        "The `prediction` function below takes a `Matrix` and a `Chains` object. It computes the mean of the sampled parameters and calculates the species with the highest probability for each observation. Note that we do not have to evaluate the `softmax` function since it does not affect the order of its inputs.\n",
        "\n",
        "```{julia}\n",
        "function prediction(x::Matrix, chain)\n",
        "    # Pull the means from each parameter's sampled values in the chain.\n",
        "    intercept_versicolor = mean(chain, :intercept_versicolor)\n",
        "    intercept_virginica = mean(chain, :intercept_virginica)\n",
        "    coefficients_versicolor = [\n",
        "        mean(chain, k) for k in MCMCChains.namesingroup(chain, :coefficients_versicolor)\n",
        "    ]\n",
        "    coefficients_virginica = [\n",
        "        mean(chain, k) for k in MCMCChains.namesingroup(chain, :coefficients_virginica)\n",
        "    ]\n",
        "\n",
        "    # Compute the index of the species with the highest probability for each observation.\n",
        "    values_versicolor = intercept_versicolor .+ x * coefficients_versicolor\n",
        "    values_virginica = intercept_virginica .+ x * coefficients_virginica\n",
        "    species_indices = [\n",
        "        argmax((0, x, y)) for (x, y) in zip(values_versicolor, values_virginica)\n",
        "    ]\n",
        "\n",
        "    return species_indices\n",
        "end;\n",
        "```\n",
        "\n",
        "Let's see how we did! We run the test matrix through the prediction function, and compute the accuracy for our prediction.\n",
        "\n",
        "```{julia}\n",
        "# Make the predictions.\n",
        "predictions = prediction(test_features, chain)\n",
        "\n",
        "# Calculate accuracy for our test set.\n",
        "mean(predictions .== testset[!, :Species_index])\n",
        "```\n",
        "\n",
        "Perhaps more important is to see the accuracy per class.\n",
        "\n",
        "```{julia}\n",
        "for s in 1:3\n",
        "    rows = testset[!, :Species_index] .== s\n",
        "    println(\"Number of `\", species[s], \"`: \", count(rows))\n",
        "    println(\n",
        "        \"Percentage of `\",\n",
        "        species[s],\n",
        "        \"` predicted correctly: \",\n",
        "        mean(predictions[rows] .== testset[rows, :Species_index]),\n",
        "    )\n",
        "end\n",
        "```\n",
        "\n",
        "This tutorial has demonstrated how to use Turing to perform Bayesian multinomial logistic regression."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}