{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Mode Estimation\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../../tutorials/docs-17-mode-estimation/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "After defining a statistical model, in addition to sampling from its distributions, one may be interested in finding the parameter values that maximise for instance the posterior distribution density function or the likelihood. This is called mode estimation. Turing provides support for two mode estimation techniques, [maximum likelihood estimation](https://en.wikipedia.org/wiki/Maximum_likelihood_estimation) (MLE) and [maximum a posterior](https://en.wikipedia.org/wiki/Maximum_a_posteriori_estimation) (MAP) estimation.\n",
        "\n",
        "To demonstrate mode estimation, let us load Turing and declare a model:\n",
        "\n",
        "```{julia}\n",
        "using Turing\n",
        "\n",
        "@model function gdemo(x)\n",
        "    s² ~ InverseGamma(2, 3)\n",
        "    m ~ Normal(0, sqrt(s²))\n",
        "\n",
        "    for i in eachindex(x)\n",
        "        x[i] ~ Normal(m, sqrt(s²))\n",
        "    end\n",
        "end\n",
        "```\n",
        "\n",
        "Once the model is defined, we can construct a model instance as we normally would:\n",
        "\n",
        "```{julia}\n",
        "# Instantiate the gdemo model with our data.\n",
        "data = [1.5, 2.0]\n",
        "model = gdemo(data)\n",
        "```\n",
        "\n",
        "Finding the maximum aposteriori or maximum likelihood parameters is as simple as\n",
        "\n",
        "```{julia}\n",
        "# Generate a MLE estimate.\n",
        "mle_estimate = maximum_likelihood(model)\n",
        "\n",
        "# Generate a MAP estimate.\n",
        "map_estimate = maximum_a_posteriori(model)\n",
        "```\n",
        "\n",
        "The estimates are returned as instances of the `ModeResult` type. It has the fields `values` for the parameter values found and `lp` for the log probability at the optimum, as well as `f` for the objective function and `optim_result` for more detailed results of the optimisation procedure.\n",
        "\n",
        "```{julia}\n",
        "@show mle_estimate.values\n",
        "@show mle_estimate.lp;\n",
        "```\n",
        "\n",
        "## Controlling the optimisation process\n",
        "\n",
        "Under the hood `maximum_likelihood` and `maximum_a_posteriori` use the [Optimization.jl](https://github.com/SciML/Optimization.jl) package, which provides a unified interface to many other optimisation packages. By default Turing typically uses the [LBFGS](https://en.wikipedia.org/wiki/Limited-memory_BFGS) method from [Optim.jl](https://github.com/JuliaNLSolvers/Optim.jl) to find the mode estimate, but we can easily change that:\n",
        "\n",
        "```{julia}\n",
        "using OptimizationOptimJL: NelderMead\n",
        "@show maximum_likelihood(model, NelderMead())\n",
        "\n",
        "using OptimizationNLopt: NLopt.LD_TNEWTON_PRECOND_RESTART\n",
        "@show maximum_likelihood(model, LD_TNEWTON_PRECOND_RESTART());\n",
        "```\n",
        "\n",
        "The above are just two examples, Optimization.jl supports [many more](https://docs.sciml.ai/Optimization/stable/).\n",
        "\n",
        "We can also help the optimisation by giving it a starting point we know is close to the final solution, or by specifying an automatic differentiation method\n",
        "\n",
        "```{julia}\n",
        "import Mooncake\n",
        "\n",
        "maximum_likelihood(\n",
        "    model, NelderMead(); initial_params=[0.1, 2], adtype=AutoMooncake()\n",
        ")\n",
        "```\n",
        "\n",
        "When providing values to arguments like `initial_params` the parameters are typically specified in the order in which they appear in the code of the model, so in this case first `s²` then `m`. More precisely it's the order returned by `Turing.Inference.getparams(model, DynamicPPL.VarInfo(model))`.\n",
        "\n",
        "We can also do constrained optimisation, by providing either intervals within which the parameters must stay, or costraint functions that they need to respect. For instance, here's how one can find the MLE with the constraint that the variance must be less than 0.01 and the mean must be between -1 and 1.:\n",
        "\n",
        "```{julia}\n",
        "maximum_likelihood(model; lb=[0.0, -1.0], ub=[0.01, 1.0])\n",
        "```\n",
        "\n",
        "The arguments for lower (`lb`) and upper (`ub`) bounds follow the arguments of `Optimization.OptimizationProblem`, as do other parameters for providing [constraints](https://docs.sciml.ai/Optimization/stable/tutorials/constraints/), such as `cons`. Any extraneous keyword arguments given to `maximum_likelihood` or `maximum_a_posteriori` are passed to `Optimization.solve`. Some often useful ones are `maxiters` for controlling the maximum number of iterations and `abstol` and `reltol` for the absolute and relative convergence tolerances:\n",
        "\n",
        "```{julia}\n",
        "badly_converged_mle = maximum_likelihood(\n",
        "    model, NelderMead(); maxiters=10, reltol=1e-9\n",
        ")\n",
        "```\n",
        "\n",
        "We can check whether the optimisation converged using the `optim_result` field of the result:\n",
        "\n",
        "```{julia}\n",
        "@show badly_converged_mle.optim_result;\n",
        "```\n",
        "\n",
        "For more details, such as a full list of possible arguments, we encourage the reader to read the docstring of the function `Turing.Optimisation.estimate_mode`, which is what `maximum_likelihood` and `maximum_a_posteriori` call, and the documentation of [Optimization.jl](https://docs.sciml.ai/Optimization/stable/).\n",
        "\n",
        "## Analyzing your mode estimate\n",
        "\n",
        "Turing extends several methods from `StatsBase` that can be used to analyze your mode estimation results. Methods implemented include `vcov`, `informationmatrix`, `coeftable`, `params`, and `coef`, among others.\n",
        "\n",
        "For example, let's examine our ML estimate from above using `coeftable`:\n",
        "\n",
        "```{julia}\n",
        "using StatsBase: coeftable\n",
        "coeftable(mle_estimate)\n",
        "```\n",
        "\n",
        "Standard errors are calculated from the Fisher information matrix (inverse Hessian of the log likelihood or log joint). Note that standard errors calculated in this way may not always be appropriate for MAP estimates, so please be cautious in interpreting them.\n",
        "\n",
        "## Sampling with the MAP/MLE as initial states\n",
        "\n",
        "You can begin sampling your chain from an MLE/MAP estimate by extracting the vector of parameter values and providing it to the `sample` function with the keyword `initial_params`. For example, here is how to sample from the full posterior using the MAP estimate as the starting point:\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "map_estimate = maximum_a_posteriori(model)\n",
        "chain = sample(model, NUTS(), 1_000; initial_params=map_estimate.values.array)\n",
        "```"
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}