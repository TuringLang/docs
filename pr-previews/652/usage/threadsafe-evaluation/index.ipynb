{
  "cells": [
    {
      "outputs": [],
      "cell_type": "code",
      "source": "# Install necessary dependencies.\nusing Pkg\nPkg.activate(; temp=true)\nPkg.add([\"Turing\", \"Random\"])",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "A common technique to speed up Julia code is to use multiple threads to run computations in parallel.\nThe Julia manual [has a section on multithreading](https://docs.julialang.org/en/v1/manual/multi-threading), which is a good introduction to the topic.\n\nWe assume that the reader is familiar with some threading constructs in Julia, and the general concept of data races.\nThis page specificaly discusses Turing's support for threadsafe model evaluation.\n\n:::{.callout-note}\nPlease note that this is a rapidly-moving topic, and things may change in future releases of Turing.\nIf you are ever unsure about what works and doesn't, please don't hesitate to ask on [Slack](https://julialang.slack.com/archives/CCYDC34A0) or [Discourse](https://discourse.julialang.org/c/domain/probprog/48)\n:::\n\n## MCMC sampling\n\nFor complete clarity, this page has nothing to do with parallel sampling of MCMC chains using\n\n```julia\nsample(model, sampler, MCMCThreads(), N, nchains)\n```\n\nThat parallelisation exists outside of the model evaluation, and thus is independent of the model contents.\nThis page only discusses threading _inside_ Turing models.\n\n## Threading in Turing models\n\nGiven that Turing models mostly contain 'plain' Julia code, one might expect that all threading constructs such as `Threads.@threads` or `Threads.@spawn` can be used inside Turing models.\n\nThis is, to some extent, true: for example, you can use threading constructs to speed up deterministic computations.\nFor example, here we use parallelism to speed up a transformation of `x`:\n\n```julia\n@model function f(y)\n    x ~ dist\n    x_transformed = similar(x)\n    Threads.@threads for i in eachindex(x)\n        x_transformed[i] = some_expensive_function(x[i])\n    end\n    y ~ some_likelihood(x_transformed)\nend\n```\n\nIn general, for code that does not involve tilde-statements (`x ~ dist`), threading works exactly as it does in regular Julia code.\n\n**However, extra care must be taken when using tilde-statements (`x ~ dist`) inside threaded blocks.**\nThe reason for this is because tilde-statements modify the internal VarInfo object used for model evaluation.\nEssentially, `x ~ dist` expands to something like\n\n```julia\nx, __varinfo__ = DynamicPPL.tilde_assume!!(..., __varinfo__)\n```\n\nand writing into `__varinfo__` is, _in general_, not threadsafe.\nThus, parallelising tilde-statements can lead to data races [as described in the Julia manual](https://docs.julialang.org/en/v1/manual/multi-threading/#Using-@threads-without-data-races).\n\n## Threaded tilde-statements\n\n**As of version 0.41, Turing only supports the use of tilde-statements inside threaded blocks when these are observations (i.e., likelihood terms).**\n\nThis means that the following code is safe to use:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "using Turing \n\n@model function threaded_obs(N)\n    x ~ Normal()\n    y = Vector{Float64}(undef, N)\n    Threads.@threads for i in 1:N\n        y[i] ~ Normal(x)\n    end\nend\n\nN = 100\ny = randn(N)\nmodel = threaded_obs(N) | (; y = y)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "Evaluating this model is threadsafe, in that Turing guarantees to provide the correct result in functions such as:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "logjoint(model, (; x = 0.0))",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "(we can compare with the true value)",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "logpdf(Normal(), 0.0) + sum(logpdf.(Normal(0.0), y))",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "When sampling, you must disable model checking, but otherwise results will be correct:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "sample(model, NUTS(), 100; check_model=false, progress=false)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "::: {.callout-warning}\n## Upcoming changes\n\nStarting from DynamicPPL 0.39, if you use tilde-statements or `@addlogprob!` inside threaded blocks, you will have to declare this upfront using:\n\n```julia\nmodel = threaded_obs() | (; y = randn(N))\nthreadsafe_model = setthreadsafe(model, true)\n```\n\nThen you can sample from `threadsafe_model` as before.\n\nThe reason for this change is because threadsafe evaluation comes with a performance cost, which can sometimes be substantial.\nIn the past, threadsafe evaluation was always enabled, i.e., this cost was *always* incurred whenever Julia was launched with more than one thread.\nHowever, this is not an appropriate way to determine whether threadsafe evaluation is needed!\n:::\n\n**On the other hand, parallelising the sampling of latent values is not supported.**\nAttempting to do this will either error or give wrong results.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "#| error: true\n@model function threaded_assume_bad(N)\n    x = Vector{Float64}(undef, N)\n    Threads.@threads for i in 1:N\n        x[i] ~ Normal()\n    end\n    return x\nend\n\nmodel = threaded_assume_bad(100)\n\n# This will throw an error (and probably a different error\n# each time it's run...)\nmodel()",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "**Note, in particular, that this means that you cannot currently use `predict` to sample new data in parallel.**\n\n:::{.callout-note}\n## Threaded `predict`\n\nSupport for threaded `predict` will be added in DynamicPPL 0.39 (see [this pull request](https://github.com/TuringLang/DynamicPPL.jl/pull/1130)).\n:::\n\nThat is, even for `threaded_obs` where `y` was originally an observed term, you _cannot_ do:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "#| error: true\nmodel = threaded_obs(N) | (; y = y)\nchn = sample(model, NUTS(), 100; check_model=false, progress=false)\n\npmodel = threaded_obs(N)  # don't condition on data\npredict(pmodel, chn)",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "## Alternatives to threaded observation\n\nAn alternative to using threaded observations is to manually calculate the log-likelihood term (which can be parallelised using any of Julia's standard mechanisms), and then _outside_ of the threaded block, [add it to the model using `@addlogprob!`]({{< meta usage-modifying-logprob >}}).\n\nFor example:",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "# Note that `y` has to be passed as an argument; you can't\n# condition on it because otherwise `y[i]` won't be defined.\n@model function threaded_obs_addlogprob(N, y)\n    x ~ Normal()\n\n    # Instead of this:\n    # Threads.@threads for i in 1:N\n    #     y[i] ~ Normal(x)\n    # end\n\n    # Do this instead:\n    lls = map(1:N) do i\n        Threads.@spawn begin\n            logpdf(Normal(x), y[i])\n        end\n    end\n    @addlogprob! sum(fetch.(lls))\nend",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "In a similar way, you can also use your favourite parallelism package, such as `FLoops.jl` or `OhMyThreads.jl`.\nSee [this Discourse post](https://discourse.julialang.org/t/parallelism-within-turing-jl-model/54064/9) for some examples.\n\nWe make no promises about the use of tilde-statements _with_ these packages (indeed it will most likely error), but as long as you use them to only parallelise regular Julia code (i.e., not tilde-statements), they will work as intended.\n\nThe main downside of this approach is:\n\n1. You can't use conditioning syntax to provide data; it has to be passed as an argument or otherwise included inside the model.\n2. You can't use `predict` to sample new data.\n\nOn the other hand, one benefit of rewriting the model this way is that sampling from this model with `MCMCThreads()` will always be reproducible.",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "using Random\nN = 100\ny = randn(N)\nmodel = threaded_obs_addlogprob(N, y)\nnuts_kwargs = (check_model=false, progress=false, verbose=false)\n\nchain1 = sample(Xoshiro(468), model, NUTS(), MCMCThreads(), 1000, 4; nuts_kwargs...)\nchain2 = sample(Xoshiro(468), model, NUTS(), MCMCThreads(), 1000, 4; nuts_kwargs...)\nmean(chain1[:x]), mean(chain2[:x])  # should be identical",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "In contrast, the original `threaded_obs` (which used tilde inside `Threads.@threads`) is not reproducible when using `MCMCThreads()`.\n(In principle, we would like to fix this bug, but we haven't yet investigated where it stems from.)",
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": "model = threaded_obs(N) | (; y = y)\nnuts_kwargs = (check_model=false, progress=false, verbose=false)\nchain1 = sample(Xoshiro(468), model, NUTS(), MCMCThreads(), 1000, 4; nuts_kwargs...)\nchain2 = sample(Xoshiro(468), model, NUTS(), MCMCThreads(), 1000, 4; nuts_kwargs...)\nmean(chain1[:x]), mean(chain2[:x])  # oops!",
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": "## AD support\n\nFinally, if you are [using Turing with automatic differentiation]({{< meta usage-automatic-differentiation >}}), you also need to keep track of which AD backends support threadsafe evaluation.\n\nForwardDiff is the only AD backend that we find to work reliably with threaded model evaluation.\n\nIn particular:\n\n - ReverseDiff sometimes gives right results, but quite often gives incorrect gradients.\n - Mooncake [currently does not support multithreading at all](https://github.com/chalk-lab/Mooncake.jl/issues/570).\n - Enzyme [mostly gives the right result, but sometimes gives incorrect gradients](https://github.com/TuringLang/DynamicPPL.jl/issues/1131).\n\n## Under the hood\n\n:::{.callout-note}\nThis part will likely only be of interest to DynamicPPL developers and the very curious user.\n:::\n\n### Why is VarInfo not threadsafe?\n\nAs alluded to above, the issue with threaded tilde-statements stems from the fact that these tilde-statements modify the VarInfo object used for model evaluation, leading to potential data races.\n\nTraditionally, VarInfo objects contain both *metadata* as well as *accumulators*.\nMetadata is where information about the random variables' values are stored.\nIt is a Dict-like structure, and pushing to it from multiple threads is therefore not threadsafe (Julia's `Dict` has similar limitations).\n\nOn the other hand, accumulators are used to store outputs of the model, such as log-probabilities\nThe way DynamicPPL's threadsafe evaluation works is to create one set of accumulators per thread, and then combine the results at the end of model evaluation.\n\nIn this way, any function call that _solely_ involving accumulators can be made threadsafe.\nFor example, this is why observations are supported: there is no need to modify metadata, and only the log-likelihood accumulator needs to be updated.\n\nHowever, `assume` tilde-statements always modify the metadata, and thus cannot currently be made threadsafe.\n\n### OnlyAccsVarInfo\n\nAs it happens, much of what is needed in DynamicPPL can be constructed such that they *only* rely on accumulators.\n\nFor example, as long as there is no need to *sample* new values of random variables, it is actually fine to completely omit the metadata object.\nThis is the case for `LogDensityFunction`: since values are provided as the input vector, there is no need to store it in metadata.\nWe need only calculate the associated log-prior probability, which is stored in an accumulator.\nThus, starting from DynamicPPL v0.39, `LogDensityFunction` itself will in fact be completely threadsafe.\n\nTechnically speaking, this is achieved using `OnlyAccsVarInfo`, which is a subtype of `VarInfo` that only contains accumulators, and no metadata at all.\nIt implements enough of the `VarInfo` interface to be used in model evaluation, but will error if any functions attempt to modify or read its metadata.\n\nThere is currently an ongoing push to use `OnlyAccsVarInfo` in as many settings as we possibly can.\nFor example, this is why `predict` will be threadsafe in DynamicPPL v0.39: instead of modifying metadata to store the predicted values, we store them inside a `ValuesAsInModelAccumulator` instead, and combine them at the end of evaluation.\n\nHowever, propagating these changes up to Turing will require a substantial amount of additional work, since there are many places in Turing which currently rely on a full VarInfo (with metadata).\nSee, e.g., [this PR](https://github.com/TuringLang/DynamicPPL.jl/pull/1154) for more information.",
      "metadata": {}
    }
  ],
  "nbformat_minor": 5,
  "metadata": {
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia"
    },
    "kernelspec": {
      "name": "julia",
      "display_name": "Julia",
      "language": "julia"
    }
  },
  "nbformat": 4
}