{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: \"A Mini Turing Implementation I: Compiler\"\n",
        "engine: julia\n",
        "aliases:\n",
        "  - ../../../tutorials/14-minituring/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "In this tutorial we develop a very simple probabilistic programming language.\n",
        "The implementation is similar to [DynamicPPL](https://github.com/TuringLang/DynamicPPL.jl).\n",
        "This is intentional as we want to demonstrate some key ideas from Turing's internal implementation.\n",
        "\n",
        "To make things easy to understand and to implement we restrict our language to a very simple subset of the language that Turing actually supports.\n",
        "Defining an accurate syntax description is not our goal here, instead, we give a simple example and all similar programs should work.\n",
        "\n",
        "# Consider a probabilistic model defined by\n",
        "\n",
        "$$\n",
        "\\begin{aligned}\n",
        "a &\\sim \\operatorname{Normal}(0.5, 1^2) \\\\\n",
        "b &\\sim \\operatorname{Normal}(a, 2^2) \\\\\n",
        "x &\\sim \\operatorname{Normal}(b, 0.5^2)\n",
        "\\end{aligned}\n",
        "$$\n",
        "\n",
        "We assume that `x` is data, i.e., an observed variable.\n",
        "In our small language this model will be defined as\n",
        "\n",
        "```{julia}\n",
        "#| eval: false\n",
        "@mini_model function m(x)\n",
        "    a ~ Normal(0.5, 1)\n",
        "    b ~ Normal(a, 2)\n",
        "    x ~ Normal(b, 0.5)\n",
        "    return nothing\n",
        "end\n",
        "```\n",
        "\n",
        "Specifically, we demand that\n",
        "\n",
        "  - all observed variables are arguments of the program,\n",
        "  - the model definition does not contain any control flow,\n",
        "  - all variables are scalars, and\n",
        "  - the function returns `nothing`.\n",
        "\n",
        "First, we import some required packages:\n",
        "\n",
        "```{julia}\n",
        "using MacroTools, Distributions, Random, AbstractMCMC, MCMCChains\n",
        "```\n",
        "\n",
        "Before getting to the actual \"compiler\", we first build the data structure for the program trace.\n",
        "A program trace for a probabilistic programming language needs to at least record the values of stochastic variables and their log-probabilities.\n",
        "\n",
        "```{julia}\n",
        "struct VarInfo{V,L}\n",
        "    values::V\n",
        "    logps::L\n",
        "end\n",
        "\n",
        "VarInfo() = VarInfo(Dict{Symbol,Float64}(), Dict{Symbol,Float64}())\n",
        "\n",
        "function Base.setindex!(varinfo::VarInfo, (value, logp), var_id)\n",
        "    varinfo.values[var_id] = value\n",
        "    varinfo.logps[var_id] = logp\n",
        "    return varinfo\n",
        "end\n",
        "```\n",
        "\n",
        "Internally, our probabilistic programming language works with two main functions:\n",
        "\n",
        "  - `assume` for sampling unobserved variables and computing their log-probabilities, and\n",
        "  - `observe` for computing log-probabilities of observed variables (but not sampling them).\n",
        "\n",
        "For different inference algorithms we may have to use different sampling procedures and different log-probability computations.\n",
        "For instance, in some cases we might want to sample all variables from their prior distributions and in other cases we might only want to compute the log-likelihood of the observations based on a given set of values for the unobserved variables.\n",
        "Thus depending on the inference algorithm we want to use different `assume` and `observe` implementations.\n",
        "We can achieve this by providing this `context` information as a function argument to `assume` and `observe`.\n",
        "\n",
        "**Note:** *Although the context system in this tutorial is inspired by DynamicPPL, it is very simplistic.\n",
        "We expand this mini Turing example in the [contexts]({{<meta contexts>}}) tutorial with some more complexity, to illustrate how and why contexts are central to Turing's design. For the full details one still needs to go to the actual source of DynamicPPL though.*\n",
        "\n",
        "Here we can see the implementation of a sampler that draws values of unobserved variables from the prior and computes the log-probability for every variable.\n",
        "\n",
        "```{julia}\n",
        "struct SamplingContext{S<:AbstractMCMC.AbstractSampler,R<:Random.AbstractRNG}\n",
        "    rng::R\n",
        "    sampler::S\n",
        "end\n",
        "\n",
        "struct PriorSampler <: AbstractMCMC.AbstractSampler end\n",
        "\n",
        "function observe(context::SamplingContext, varinfo, dist, var_id, var_value)\n",
        "    logp = logpdf(dist, var_value)\n",
        "    varinfo[var_id] = (var_value, logp)\n",
        "    return nothing\n",
        "end\n",
        "\n",
        "function assume(context::SamplingContext{PriorSampler}, varinfo, dist, var_id)\n",
        "    sample = Random.rand(context.rng, dist)\n",
        "    logp = logpdf(dist, sample)\n",
        "    varinfo[var_id] = (sample, logp)\n",
        "    return sample\n",
        "end;\n",
        "```\n",
        "\n",
        "Next we define the \"compiler\" for our simple programming language.\n",
        "The term compiler is actually a bit misleading here since its only purpose is to transform the function definition in the `@mini_model` macro by\n",
        "\n",
        "  - adding the context information (`context`) and the tracing data structure (`varinfo`) as additional arguments, and\n",
        "  - replacing tildes with calls to `assume` and `observe`.\n",
        "\n",
        "Afterwards, as usual the Julia compiler will just-in-time compile the model function when it is called.\n",
        "\n",
        "The manipulation of Julia expressions is an advanced part of the Julia language.\n",
        "The [Julia documentation](https://docs.julialang.org/en/v1/manual/metaprogramming/) provides an introduction to and more details about this so-called metaprogramming.\n",
        "\n",
        "```{julia}\n",
        "macro mini_model(expr)\n",
        "    return esc(mini_model(expr))\n",
        "end\n",
        "\n",
        "function mini_model(expr)\n",
        "    # Split the function definition into a dictionary with its name, arguments, body etc.\n",
        "    def = MacroTools.splitdef(expr)\n",
        "\n",
        "    # Replace tildes in the function body with calls to `assume` or `observe`\n",
        "    def[:body] = MacroTools.postwalk(def[:body]) do sub_expr\n",
        "        if MacroTools.@capture(sub_expr, var_ ~ dist_)\n",
        "            if var in def[:args]\n",
        "                # If the variable is an argument of the model function, it is observed\n",
        "                return :($(observe)(context, varinfo, $dist, $(Meta.quot(var)), $var))\n",
        "            else\n",
        "                # Otherwise it is unobserved\n",
        "                return :($var = $(assume)(context, varinfo, $dist, $(Meta.quot(var))))\n",
        "            end\n",
        "        else\n",
        "            return sub_expr\n",
        "        end\n",
        "    end\n",
        "\n",
        "    # Add `context` and `varinfo` arguments to the model function\n",
        "    def[:args] = vcat(:varinfo, :context, def[:args])\n",
        "\n",
        "    # Reassemble the function definition from its name, arguments, body etc.\n",
        "    return MacroTools.combinedef(def)\n",
        "end;\n",
        "```\n",
        "\n",
        "For inference, we make use of the [AbstractMCMC interface](https://turinglang.github.io/AbstractMCMC.jl/dev/).\n",
        "It provides a default implementation of a `sample` function for sampling a Markov chain.\n",
        "The default implementation already supports e.g. sampling of multiple chains in parallel, thinning of samples, or discarding initial samples.\n",
        "\n",
        "The AbstractMCMC interface requires us to at least\n",
        "\n",
        "  - define a model that is a subtype of `AbstractMCMC.AbstractModel`,\n",
        "  - define a sampler that is a subtype of `AbstractMCMC.AbstractSampler`,\n",
        "  - implement `AbstractMCMC.step` for our model and sampler.\n",
        "\n",
        "Thus here we define a `MiniModel` model.\n",
        "In this model we store the model function and the observed data.\n",
        "\n",
        "```{julia}\n",
        "struct MiniModel{F,D} <: AbstractMCMC.AbstractModel\n",
        "    f::F\n",
        "    data::D # a NamedTuple of all the data\n",
        "end\n",
        "```\n",
        "\n",
        "In the Turing compiler, the model-specific `DynamicPPL.Model` is constructed automatically when calling the model function.\n",
        "But for the sake of simplicity here we construct the model manually.\n",
        "\n",
        "To illustrate probabilistic inference with our mini language we implement an extremely simplistic Random-Walk Metropolis-Hastings sampler.\n",
        "We hard-code the proposal step as part of the sampler and only allow normal distributions with zero mean and fixed standard deviation.\n",
        "The Metropolis-Hastings sampler in Turing is more flexible.\n",
        "\n",
        "```{julia}\n",
        "struct MHSampler{T<:Real} <: AbstractMCMC.AbstractSampler\n",
        "    sigma::T\n",
        "end\n",
        "\n",
        "MHSampler() = MHSampler(1)\n",
        "\n",
        "function assume(context::SamplingContext{<:MHSampler}, varinfo, dist, var_id)\n",
        "    sampler = context.sampler\n",
        "    old_value = varinfo.values[var_id]\n",
        "\n",
        "    # propose a random-walk step, i.e, add the current value to a random\n",
        "    # value sampled from a Normal distribution centered at 0\n",
        "    value = rand(context.rng, Normal(old_value, sampler.sigma))\n",
        "    logp = Distributions.logpdf(dist, value)\n",
        "    varinfo[var_id] = (value, logp)\n",
        "\n",
        "    return value\n",
        "end;\n",
        "```\n",
        "\n",
        "We need to define two `step` functions, one for the first step and the other for the following steps.\n",
        "In the first step we sample values from the prior distributions and in the following steps we sample with the random-walk proposal.\n",
        "The two functions are identified by the different arguments they take.\n",
        "\n",
        "```{julia}\n",
        "# The fist step: Sampling from the prior distributions\n",
        "function AbstractMCMC.step(\n",
        "    rng::Random.AbstractRNG, model::MiniModel, sampler::MHSampler; kwargs...\n",
        ")\n",
        "    vi = VarInfo()\n",
        "    ctx = SamplingContext(rng, PriorSampler())\n",
        "    model.f(vi, ctx, values(model.data)...)\n",
        "    return vi, vi\n",
        "end\n",
        "\n",
        "# The following steps: Sampling with random-walk proposal\n",
        "function AbstractMCMC.step(\n",
        "    rng::Random.AbstractRNG,\n",
        "    model::MiniModel,\n",
        "    sampler::MHSampler,\n",
        "    prev_state::VarInfo; # is just the old trace\n",
        "    kwargs...,\n",
        ")\n",
        "    vi = prev_state\n",
        "    new_vi = deepcopy(vi)\n",
        "    ctx = SamplingContext(rng, sampler)\n",
        "    model.f(new_vi, ctx, values(model.data)...)\n",
        "\n",
        "    # Compute log acceptance probability\n",
        "    # Since the proposal is symmetric the computation can be simplified\n",
        "    logα = sum(values(new_vi.logps)) - sum(values(vi.logps))\n",
        "\n",
        "    # Accept proposal with computed acceptance probability\n",
        "    if -randexp(rng) < logα\n",
        "        return new_vi, new_vi\n",
        "    else\n",
        "        return prev_state, prev_state\n",
        "    end\n",
        "end;\n",
        "```\n",
        "\n",
        "To make it easier to analyze the samples and compare them with results from Turing, additionally we define a version of `AbstractMCMC.bundle_samples` for our model and sampler that returns a `MCMCChains.Chains` object of samples.\n",
        "\n",
        "```{julia}\n",
        "function AbstractMCMC.bundle_samples(\n",
        "    samples, model::MiniModel, ::MHSampler, ::Any, ::Type{Chains}; kwargs...\n",
        ")\n",
        "    # We get a vector of traces\n",
        "    values = [sample.values for sample in samples]\n",
        "    params = [key for key in keys(values[1]) if key ∉ keys(model.data)]\n",
        "    vals = reduce(hcat, [value[p] for value in values] for p in params)\n",
        "    # Composing the `Chains` data-structure, of which analyzing infrastructure is provided\n",
        "    chains = Chains(vals, params)\n",
        "    return chains\n",
        "end;\n",
        "```\n",
        "\n",
        "Let us check how our mini probabilistic programming language works.\n",
        "We define the probabilistic model:\n",
        "\n",
        "```{julia}\n",
        "@mini_model function m(x)\n",
        "    a ~ Normal(0.5, 1)\n",
        "    b ~ Normal(a, 2)\n",
        "    x ~ Normal(b, 0.5)\n",
        "    return nothing\n",
        "end;\n",
        "```\n",
        "\n",
        "The `@mini_model` macro expands this into another function, `m`, which effectively calls either `assume` or `observe` on each variable as needed:\n",
        "\n",
        "```{julia}\n",
        "@macroexpand @mini_model function m(x)\n",
        "    a ~ Normal(0.5, 1)\n",
        "    b ~ Normal(a, 2)\n",
        "    x ~ Normal(b, 0.5)\n",
        "    return nothing\n",
        "end\n",
        "```\n",
        "\n",
        "We can use this function to construct the `MiniModel`, and then perform inference with data `x = 3.0`:\n",
        "\n",
        "```{julia}\n",
        "sample(MiniModel(m, (x=3.0,)), MHSampler(), 1_000_000; chain_type=Chains, progress=false)\n",
        "```\n",
        "\n",
        "We compare these results with Turing.\n",
        "\n",
        "```{julia}\n",
        "using Turing\n",
        "using PDMats\n",
        "\n",
        "@model function turing_m(x)\n",
        "    a ~ Normal(0.5, 1)\n",
        "    b ~ Normal(a, 2)\n",
        "    x ~ Normal(b, 0.5)\n",
        "    return nothing\n",
        "end\n",
        "\n",
        "sample(turing_m(3.0), MH(ScalMat(2, 1.0)), 1_000_000, progress=false)\n",
        "```\n",
        "\n",
        "As you can see, with our simple probabilistic programming language and custom samplers we get similar results as Turing."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}