{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: Bayesian Logistic Regression\n",
        "engine: julia\n",
        "aliases:\n",
        " - ../02-logistic-regression/index.html\n",
        "---\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "#| output: false\n",
        "using Pkg;\n",
        "Pkg.instantiate();\n",
        "```\n",
        "\n",
        "[Bayesian logistic regression](https://en.wikipedia.org/wiki/Logistic_regression#Bayesian) is the Bayesian counterpart to a common tool in machine learning, logistic regression.\n",
        "The goal of logistic regression is to predict a one or a zero for a given training item.\n",
        "An example might be predicting whether someone is sick or ill given their symptoms and personal information.\n",
        "\n",
        "In our example, we'll be working to predict whether someone is likely to default with a synthetic dataset found in the `RDatasets` package. This dataset, `Defaults`, comes from R's [ISLR](https://cran.r-project.org/web/packages/ISLR/index.html) package and contains information on borrowers.\n",
        "\n",
        "To start, let's import all the libraries we'll need.\n",
        "\n",
        "```{julia}\n",
        "# Import Turing and Distributions.\n",
        "using Turing, Distributions\n",
        "\n",
        "# Import RDatasets.\n",
        "using RDatasets\n",
        "\n",
        "# Import MCMCChains, Plots, and StatsPlots for visualizations and diagnostics.\n",
        "using MCMCChains, Plots, StatsPlots\n",
        "\n",
        "# We need a logistic function, which is provided by StatsFuns.\n",
        "using StatsFuns: logistic\n",
        "\n",
        "# Functionality for splitting and normalizing the data\n",
        "using MLDataUtils: shuffleobs, stratifiedobs, rescale!\n",
        "\n",
        "# Set a seed for reproducibility.\n",
        "using Random\n",
        "Random.seed!(0);\n",
        "```\n",
        "\n",
        "## Data Cleaning & Set Up\n",
        "\n",
        "Now we're going to import our dataset. The first six rows of the dataset are shown below so you can get a good feel for what kind of data we have.\n",
        "\n",
        "```{julia}\n",
        "# Import the \"Default\" dataset.\n",
        "data = RDatasets.dataset(\"ISLR\", \"Default\");\n",
        "\n",
        "# Show the first six rows of the dataset.\n",
        "first(data, 6)\n",
        "```\n",
        "\n",
        "Most machine learning processes require some effort to tidy up the data, and this is no different. We need to convert the `Default` and `Student` columns, which say \"Yes\" or \"No\" into 1s and 0s. Afterwards, we'll get rid of the old words-based columns.\n",
        "\n",
        "```{julia}\n",
        "# Convert \"Default\" and \"Student\" to numeric values.\n",
        "data[!, :DefaultNum] = [r.Default == \"Yes\" ? 1.0 : 0.0 for r in eachrow(data)]\n",
        "data[!, :StudentNum] = [r.Student == \"Yes\" ? 1.0 : 0.0 for r in eachrow(data)]\n",
        "\n",
        "# Delete the old columns which say \"Yes\" and \"No\".\n",
        "select!(data, Not([:Default, :Student]))\n",
        "\n",
        "# Show the first six rows of our edited dataset.\n",
        "first(data, 6)\n",
        "```\n",
        "\n",
        "After we've done that tidying, it's time to split our dataset into training and testing sets, and separate the labels from the data. We separate our data into two halves, `train` and `test`. You can use a higher percentage of splitting (or a lower one) by modifying the `at = 0.05` argument. We have highlighted the use of only a 5% sample to show the power of Bayesian inference with small sample sizes.\n",
        "\n",
        "We must rescale our variables so that they are centered around zero by subtracting each column by the mean and dividing it by the standard deviation. Without this step, Turing's sampler will have a hard time finding a place to start searching for parameter estimates. To do this we will leverage `MLDataUtils`, which also lets us effortlessly shuffle our observations and perform a stratified split to get a representative test set.\n",
        "\n",
        "```{julia}\n",
        "function split_data(df, target; at=0.70)\n",
        "    shuffled = shuffleobs(df)\n",
        "    return trainset, testset = stratifiedobs(row -> row[target], shuffled; p=at)\n",
        "end\n",
        "\n",
        "features = [:StudentNum, :Balance, :Income]\n",
        "numerics = [:Balance, :Income]\n",
        "target = :DefaultNum\n",
        "\n",
        "trainset, testset = split_data(data, target; at=0.05)\n",
        "for feature in numerics\n",
        "    μ, σ = rescale!(trainset[!, feature]; obsdim=1)\n",
        "    rescale!(testset[!, feature], μ, σ; obsdim=1)\n",
        "end\n",
        "\n",
        "# Turing requires data in matrix form, not dataframe\n",
        "train = Matrix(trainset[:, features])\n",
        "test = Matrix(testset[:, features])\n",
        "train_label = trainset[:, target]\n",
        "test_label = testset[:, target];\n",
        "```\n",
        "\n",
        "## Model Declaration\n",
        "\n",
        "Finally, we can define our model.\n",
        "\n",
        "`logistic_regression` takes four arguments:\n",
        "\n",
        "  - `x` is our set of independent variables;\n",
        "  - `y` is the element we want to predict;\n",
        "  - `n` is the number of observations we have; and\n",
        "  - `σ` is the standard deviation we want to assume for our priors.\n",
        "\n",
        "Within the model, we create four coefficients (`intercept`, `student`, `balance`, and `income`) and assign a prior of normally distributed with means of zero and standard deviations of `σ`. We want to find values of these four coefficients to predict any given `y`.\n",
        "\n",
        "The `for` block creates a variable `v` which is the logistic function. We then observe the likelihood of calculating `v` given the actual label, `y[i]`.\n",
        "\n",
        "```{julia}\n",
        "# Bayesian logistic regression (LR)\n",
        "@model function logistic_regression(x, y, n, σ)\n",
        "    intercept ~ Normal(0, σ)\n",
        "\n",
        "    student ~ Normal(0, σ)\n",
        "    balance ~ Normal(0, σ)\n",
        "    income ~ Normal(0, σ)\n",
        "\n",
        "    for i in 1:n\n",
        "        v = logistic(intercept + student * x[i, 1] + balance * x[i, 2] + income * x[i, 3])\n",
        "        y[i] ~ Bernoulli(v)\n",
        "    end\n",
        "end;\n",
        "```\n",
        "\n",
        "## Sampling\n",
        "\n",
        "Now we can run our sampler. This time we'll use [`NUTS`](https://turinglang.org/stable/docs/library/#Turing.Inference.NUTS) to sample from our posterior.\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "setprogress!(false)\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| output: false\n",
        "# Retrieve the number of observations.\n",
        "n, _ = size(train)\n",
        "\n",
        "# Sample using NUTS.\n",
        "m = logistic_regression(train, train_label, n, 1)\n",
        "chain = sample(m, NUTS(), MCMCThreads(), 1_500, 3)\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "chain\n",
        "```\n",
        "\n",
        "::: {.callout-warning collapse=\"true\"}\n",
        "## Sampling With Multiple Threads\n",
        "The `sample()` call above assumes that you have at least `nchains` threads available in your Julia instance. If you do not, the multiple chains \n",
        "will run sequentially, and you may notice a warning. For more information, see [the Turing documentation on sampling multiple chains.]({{<meta core-functionality>}}#sampling-multiple-chains)\n",
        ":::\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "let\n",
        "    mean_params = mean(chain)\n",
        "    @assert mean_params[:student, :mean] < 0.1\n",
        "    @assert mean_params[:balance, :mean] > 1\n",
        "end\n",
        "```\n",
        "\n",
        "Since we ran multiple chains, we may as well do a spot check to make sure each chain converges around similar points.\n",
        "\n",
        "```{julia}\n",
        "plot(chain)\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "let\n",
        "    mean_params = mapreduce(hcat, mean(chain; append_chains=false)) do df\n",
        "        return df[:, :mean]\n",
        "    end\n",
        "    for i in (2, 3)\n",
        "        @assert mean_params[:, i] != mean_params[:, 1]\n",
        "        @assert isapprox(mean_params[:, i], mean_params[:, 1]; rtol=5e-2)\n",
        "    end\n",
        "end\n",
        "```\n",
        "\n",
        "Looks good!\n",
        "\n",
        "We can also use the `corner` function from MCMCChains to show the distributions of the various parameters of our logistic regression.\n",
        "\n",
        "```{julia}\n",
        "# The labels to use.\n",
        "l = [:student, :balance, :income]\n",
        "\n",
        "# Use the corner function. Requires StatsPlots and MCMCChains.\n",
        "corner(chain, l)\n",
        "```\n",
        "\n",
        "Fortunately the corner plot appears to demonstrate unimodal distributions for each of our parameters, so it should be straightforward to take the means of each parameter's sampled values to estimate our model to make predictions.\n",
        "\n",
        "## Making Predictions\n",
        "\n",
        "How do we test how well the model actually predicts whether someone is likely to default? We need to build a prediction function that takes the `test` object we made earlier and runs it through the average parameter calculated during sampling.\n",
        "\n",
        "The `prediction` function below takes a `Matrix` and a `Chain` object. It takes the mean of each parameter's sampled values and re-runs the logistic function using those mean values for every element in the test set.\n",
        "\n",
        "```{julia}\n",
        "function prediction(x::Matrix, chain, threshold)\n",
        "    # Pull the means from each parameter's sampled values in the chain.\n",
        "    intercept = mean(chain[:intercept])\n",
        "    student = mean(chain[:student])\n",
        "    balance = mean(chain[:balance])\n",
        "    income = mean(chain[:income])\n",
        "\n",
        "    # Retrieve the number of rows.\n",
        "    n, _ = size(x)\n",
        "\n",
        "    # Generate a vector to store our predictions.\n",
        "    v = Vector{Float64}(undef, n)\n",
        "\n",
        "    # Calculate the logistic function for each element in the test set.\n",
        "    for i in 1:n\n",
        "        num = logistic(\n",
        "            intercept .+ student * x[i, 1] + balance * x[i, 2] + income * x[i, 3]\n",
        "        )\n",
        "        if num >= threshold\n",
        "            v[i] = 1\n",
        "        else\n",
        "            v[i] = 0\n",
        "        end\n",
        "    end\n",
        "    return v\n",
        "end;\n",
        "```\n",
        "\n",
        "Let's see how we did! We run the test matrix through the prediction function, and compute the [mean squared error](https://en.wikipedia.org/wiki/Mean_squared_error) (MSE) for our prediction. The `threshold` variable sets the sensitivity of the predictions. For example, a threshold of 0.07 will predict a defualt value of 1 for any predicted value greater than 0.07 and no default if it is less than 0.07.\n",
        "\n",
        "```{julia}\n",
        "# Set the prediction threshold.\n",
        "threshold = 0.07\n",
        "\n",
        "# Make the predictions.\n",
        "predictions = prediction(test, chain, threshold)\n",
        "\n",
        "# Calculate MSE for our test set.\n",
        "loss = sum((predictions - test_label) .^ 2) / length(test_label)\n",
        "```\n",
        "\n",
        "Perhaps more important is to see what percentage of defaults we correctly predicted. The code below simply counts defaults and predictions and presents the results.\n",
        "\n",
        "```{julia}\n",
        "defaults = sum(test_label)\n",
        "not_defaults = length(test_label) - defaults\n",
        "\n",
        "predicted_defaults = sum(test_label .== predictions .== 1)\n",
        "predicted_not_defaults = sum(test_label .== predictions .== 0)\n",
        "\n",
        "println(\"Defaults: $defaults\n",
        "    Predictions: $predicted_defaults\n",
        "    Percentage defaults correct $(predicted_defaults/defaults)\")\n",
        "\n",
        "println(\"Not defaults: $not_defaults\n",
        "    Predictions: $predicted_not_defaults\n",
        "    Percentage non-defaults correct $(predicted_not_defaults/not_defaults)\")\n",
        "```\n",
        "\n",
        "```{julia}\n",
        "#| echo: false\n",
        "let\n",
        "    percentage_correct = predicted_defaults / defaults\n",
        "    @assert 0.6 < percentage_correct\n",
        "end\n",
        "```\n",
        "\n",
        "The above shows that with a threshold of 0.07, we correctly predict a respectable portion of the defaults, and correctly identify most non-defaults. This is fairly sensitive to a choice of threshold, and you may wish to experiment with it.\n",
        "\n",
        "This tutorial has demonstrated how to use Turing to perform Bayesian logistic regression."
      ]
    }
  ],
  "metadata": {
    "kernelspec": {
      "display_name": "Python 3",
      "language": "python",
      "name": "python3"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 4
}